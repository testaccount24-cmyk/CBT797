         MACRO ,
&NAME    #BLKLEN &DECB,&REG,&WKREG=R15,&ACSMETH=BSAM
         LCLC  &A,&B,&R,&O,&F
&O       SETC  '4'                 FLAG OFFSET FOR BSAM
&F       SETC  '128'               'S' FLAG FOR BSAM
         AIF   ('&ACSMETH' EQ 'BSAM' OR '&ACSMETH' EQ 'BPAM').BLK0100
         AIF   ('&ACSMETH' NE 'BDAM').BLK0600
&O       SETC  '5'                 FLAG OFFSET FOR BDAM
&F       SETC  '64'                'S' FLAG FOR BDAM
.BLK0100 ANOP  ,
.*
&R       SETC  '&REG'
         AIF   ('&REG' NE '').BLK0200
&R       SETC  'R1'
.*
.BLK0200 AIF   ('&DECB' EQ '').BLK0500
         AIF   ('&DECB'(1,1) EQ '(').BLK0300
&A       SETC  '&DECB+'
&B       SETC  ''
         AGO   .BLK0400
.BLK0300 ANOP  ,
&A       SETC  ''
&B       SETC  '&DECB'
.*
.BLK0400 ANOP  ,
&NAME    LH    &R,&A.6&B                GET BLKSIZE FROM DECB
         TM    &A.&O&B,&F               WAS 'S' CODED ON READ ?
         BZ    *+12                     YES, SKIP DCB STUFF
         L     &R,&A.8&B                GET DCB ADDRESS
         LH    &R,62(0,&R)              GET BLKSIZE FROM DCB
         L     &WKREG,&A.16&B           LOAD IOB ADDRESS
         SH    &R,14(0,&WKREG)          GET ACTUAL # BYTES READ
         MEXIT EXIT
.BLK0500 MNOTE 8,'BLK001A - DECB Address Missing'
         MEXIT EXIT
.BLK0600 MNOTE 4,'BLK002A - Invalid Access Method, BSAM Assumed'
         AGO   .BLK0100                 CONTINUE
         MEND  ,
