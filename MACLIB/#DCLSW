         MACRO ,
&NAME    #DCLSW &INIT=0,&MF=D,&GENERIC=
         GBLA  &DCLDSW#             COUNTER OF # OF SWITCH BYTES
         GBLA  &DCLGSW#             COUNTER OF # OF GENERIC BYTES
         GBLB  &DCLDSWD(99)         DEFER DECLARE INDICATIORS
         GBLC  &DCLDSWN(99)         SWITCH BYTE NAMES
         GBLC  &DCLDSWB(800)        SWITCH BIT NAMES
         GBLC  &DCLGSWN(99)         GENERIC SWITCH BYTE NAMES
         GBLC  &DCLGSWB(99)         GENERIC SWITCH BIT PREFIXES
         LCLA  &A,&B,&C,&D,&E       LOCAL WORK SYMBOLS
         LCLB  &F,&G
         LCLC  &K,&L,&M,&P
         AIF   ('&GENERIC' EQ '').DCL0200   NO GENERIC, CONTINUE
.*
.* THE FOLLOWING IS FOR THE GENERIC= OPTION
.*
&DCLGSW# SETA  &DCLGSW#+1           INCREMENT GENERIC SWITCH NUMBER
&DCLGSWN(&DCLGSW#) SETC '&NAME'     SET SWITCH NAME
&DCLGSWB(&DCLGSW#) SETC '&GENERIC'   SET GENERIC PREFIX
         AIF   ('&NAME' NE '').DCL0100   THERE WAS A NAME, EXIT
&DCLGSWN(&DCLGSW#) SETC '&GENERIC'   SET SWITCH NAME TO PREFIX
.DCL0100 MEXIT                       EXIT
.DCL0200 AIF   ('&MF' NE 'L').DCL2300   NOT MF=L, CONTINUE
.*
.* THE FOLLOWING IS FOR MF=L
.*
         AIF   ('&NAME' EQ '').DCL1200   IF NO NAME, DO ALL
&A       SETA  0                    START OF LOOP COUNTER
.DCL0300 AIF   (&A GE &DCLDSW#).DCL3400   ERROR - SWITCH NOT FOUND
&A       SETA  &A+1                 GET NEXT SWITCH
         AIF   ('&NAME' NE '&DCLDSWN(&A)').DCL0300   NO MATCH, LOOP
         AIF   (NOT &DCLDSWD(&A)).DCL3500   ERROR - NOT DEFERRED
&DCLDSWD(&A) SETB 0                 RESET DEFER BIT
&K       SETC  '&DCLDSWN(&A)'       GET SWITCH BYTE NAME
&K       DC    AL1(&INIT)           #DCLSW MACRO, JUNE 29TH, 1979
&C       SETA  &A*8-7               INDEX TO FIRST SWITCH BIT NAME
&L       SETC  '&DCLDSWB(&C+0)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0400   NULL, CONTINUE
&L       EQU   X'80'                1... ...
.DCL0400 ANOP
&L       SETC  '&DCLDSWB(&C+1)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0500   NULL, CONTINUE
&L       EQU   X'40'                .1.. ...
.DCL0500 ANOP
&L       SETC  '&DCLDSWB(&C+2)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0600   NULL, CONTINUE
&L       EQU   X'20'                ..1. ...
.DCL0600 ANOP
&L       SETC  '&DCLDSWB(&C+3)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0700   NULL, CONTINUE
&L       EQU   X'10'                ...1 ...
.DCL0700 ANOP
&L       SETC  '&DCLDSWB(&C+4)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0800   NULL, CONTINUE
&L       EQU   X'08'                .... 1..
.DCL0800 ANOP
&L       SETC  '&DCLDSWB(&C+5)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL0900   NULL, CONTINUE
&L       EQU   X'04'                .... .1.
.DCL0900 ANOP
&L       SETC  '&DCLDSWB(&C+6)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1000   NULL, CONTINUE
&L       EQU   X'02'                .... ..1
.DCL1000 ANOP
&L       SETC  '&DCLDSWB(&C+7)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1100   NULL, CONTINUE
&L       EQU   X'01'                .... ...1
.DCL1100 MEXIT                      EXIT
.DCL1200 ANOP
&A       SETA  0                    INDEX OF SWITCH BYTES
&B       SETA  &DCLDSW#             GET NUMBER OF SWITCHES
.DCL1300 AIF   (&A GE &B).DCL2200   FINISHED, EXIT
&A       SETA  &A+1                 POINT TO NEXT ONE
         AIF   (NOT &DCLDSWD(&A)).DCL1300   SKIP IF DEFER BIT NOT ON
&DCLDSWD(&A) SETB 0                 RESET DEFER BIT
&K       SETC  '&DCLDSWN(&A)'       GET SWITCH BYTE NAME
&K       DC    AL1(&INIT)           #DCLSW MACRO, JUNE 29TH, 1979
&C       SETA  &A*8-7               POINT TO FIRST SWITCH BIT NAME
&L       SETC  '&DCLDSWB(&C+0)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1400   NULL, CONTINUE
&L       EQU   X'80'                1... ...
.DCL1400 ANOP
&L       SETC  '&DCLDSWB(&C+1)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1500   NULL, CONTINUE
&L       EQU   X'40'                .1.. ...
.DCL1500 ANOP
&L       SETC  '&DCLDSWB(&C+2)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1600   NULL, CONTINUE
&L       EQU   X'20'                ..1. ...
.DCL1600 ANOP
&L       SETC  '&DCLDSWB(&C+3)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1700   NULL, CONTINUE
&L       EQU   X'10'                ...1 ...
.DCL1700 ANOP
&L       SETC  '&DCLDSWB(&C+4)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1800   NULL, CONTINUE
&L       EQU   X'08'                .... 1..
.DCL1800 ANOP
&L       SETC  '&DCLDSWB(&C+5)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL1900   NULL, CONTINUE
&L       EQU   X'04'                .... .1.
.DCL1900 ANOP
&L       SETC  '&DCLDSWB(&C+6)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL2000   NULL, CONTINUE
&L       EQU   X'02'                .... ..1
.DCL2000 ANOP
&L       SETC  '&DCLDSWB(&C+7)'     GET SWITCH BIT NAME
         AIF   ('&L' EQ '').DCL2100   NULL, CONTINUE
&L       EQU   X'01'                .... ...1
.DCL2100 AGO   .DCL1300             LOOP BACK TO DO NEXT SWITCH
.DCL2200 MEXIT                      FINISHED, EXIT
.DCL2300 AIF   ('&MF' NE 'E').DCL2600   NOT MF=E, CONTINUE
.*
.* THE FOLLOWING IS FOR MF=E
.*
&A       SETA  0                    INDEX INTO POSITIONAL PARMS
&B       SETA  N'&SYSLIST           GET NUMBER OF SWITCH BITS
         AIF   (&B EQ 0).DCL3600    ERROR - NO SWITCH BITS PRESENT
         AIF   ('&NAME' EQ '').DCL3700   ERROR - NO NAME SUPPLIED
&DCLDSW# SETA  &DCLDSW#+1           POINT TO NEXT SWITCH BYTE NAME
&C       SETA  &DCLDSW#*8-7         POINT TO FIRST SWITCH BIT NAME
&DCLDSWN(&DCLDSW#) SETC '&NAME'     SAVE SWITCH BYTE NAME
         AIF   (&B LT 9).DCL2400    LESS THAN 9 SWITCHES, CONTINUE
         MNOTE 4,'DCL001I - Only First 8 Switches Used'
&B       SETA  8                    SET MAX TO 8
.DCL2400 ANOP
&A       SETA  &A+1                 POINT TO NEXT SWITCH
         AIF   (&A GT &B).DCL2500   FINISHED, EXIT
&K       SETC  '&SYSLIST(&A)'       GET SWITCH BIT NAME
         AIF   ('&K' EQ '').DCL2400   A NULL, IGNORE IT
&DCLDSWB(&A+&C) SETC '&K'           SAVE SWITCH BIT NAME
         AGO   .DCL2400             LOOP THRU ALL 8 SWITCH BIT NAMES
.DCL2500 MEXIT                      EXIT
.DCL2600 AIF   ('&MF' EQ 'D').DCL2700   MF=D, CONTINUE
         MNOTE 4,'DCL002I - MF=&MF Invalid, MF=D Assumed'
.DCL2700 ANOP
.*
.* THE FOLLOWING IS FOR MF=D
.*
&A       SETA  0                    INDEX INTO POSITIONAL PARMS
&B       SETA  N'&SYSLIST           GET NUMBER OF SWITCH BITS
         AIF   (&B EQ 0).DCL3600    ERROR - NO SWITCH BITS PRESENT
         AIF   ('&NAME' EQ '').DCL3000   NO NAME SUPPLIED, CONTINUE
&DCLDSW# SETA  &DCLDSW#+1           POINT TO NEXT SWITCH BYTE NAME
&C       SETA  &DCLDSW#*8-8         POINT TO FIRST SWITCH BIT NAME
&DCLDSWN(&DCLDSW#) SETC '&NAME'     SAVE SWITCH BYTE NAME
&DCLDSWD(&DCLDSW#) SETB 1           SET DEFER BIT ON
         AIF   (&B LT 9).DCL2800    LESS THAN 9 SWITCHES, CONTINUE
         MNOTE 4,'DCL001I - Only First 8 Switches Used'
&B       SETA  8                    SET MAX TO 8
.DCL2800 ANOP
&A       SETA  &A+1                 POINT TO NEXT SWITCH
         AIF   (&A GT &B).DCL2900   FINISHED, EXIT
&K       SETC  '&SYSLIST(&A)'       GET SWITCH BIT NAME
         AIF   ('&K' EQ '').DCL2800   A NULL, IGNORE IT
&DCLDSWB(&A+&C) SETC '&K'           SAVE SWITCH BIT NAME
         AGO   .DCL2800             LOOP THRU ALL 8 SWITCH BIT NAMES
.DCL2900 MEXIT                      EXIT
.DCL3000 ANOP
&C       SETA  0                    BIT NUMBER, IN A BYTE
.DCL3100 ANOP
&E       SETA  &E+1                 GENERATED SWITCH BYTE NUMBER
&P       SETC  'DCL&SYSNDX&E'       GENERATE A SWITCH BYTE NAME
&DCLDSW# SETA  &DCLDSW#+1           POINT TO NEXT SWITCH BYTE NAME
&C       SETA  0                    INDEX OF BITS
&DCLDSWN(&DCLDSW#) SETC '&P'        SAVE SWITCH BYTE NAME
&DCLDSWD(&DCLDSW#) SETB 1           SET DEFER BIT ON
.DCL3200 AIF   (&C EQ 8).DCL3100    NEED NEW BYTE, GO GET IT
&C       SETA  &C+1                 POINT TO NEXT BIT
&A       SETA  &A+1                 POINT TO NEXT SWITCH
&K       SETC  '&SYSLIST(&A)'       GET SWITCH BIT NAME
         AIF   ('&K' EQ '').DCL3300   A NULL, IGNORE IT
&DCLDSWB(&DCLDSW#*8-8+&C) SETC '&K'   SAVE SWITCH BIT NAME
.DCL3300 AIF   (&A LT &B).DCL3200   LOOP THRU ALL BITS
         MEXIT                      EXIT
.DCL3400 MNOTE 8,'DCL003A - Switch "&NAME" Not Found'
         MEXIT
.DCL3500 MNOTE 8,'DCL004A - Switch "&NAME" Not Previously Deferred'   *
         MEXIT
.DCL3600 MNOTE 8,'DCL005A - No Switches Present'
         MEXIT
.DCL3700 MNOTE 8,'DCL006A - MF=E Requires A Name To Be Supplied'
         MEND
