         MACRO
&NAME    S99FAIL &RB=(R14),&RC=(R15),&CPPL=,&MF=G,&CP=,&TYPE=ALLOC
         LCLC  &LBL,&REG
&NAME    #NULL ,
         AIF   ('&MF(1)' EQ 'E').FAIL020
         AIF   ('&MF(1)' EQ 'G').FAIL010
         MNOTE 4,'&MF(1) IS AN INVALID MF, MF=G USED'
.FAIL010 ANOP  ,
         AIF   ('&MF(2)' NE '').FAIL030
         LA    R1,FAIL&SYSNDX      A(DAIRFAIL PARAMETER LIST)
         AGO   .FAIL050
.FAIL020 ANOP  ,
         AIF   ('&MF(2)' NE '').FAIL030
         MNOTE 8,'PARAMETER LIST ADDRESS NOT SPECIFIED'
         MEXIT
.FAIL030 ANOP  ,
         AIF   ('&MF(2)' EQ '(R1)' OR '&MF(2)' EQ '(1)').FAIL050
         AIF   ('&MF(2)'(1,1) EQ '(').FAIL040
         LA    R1,&MF(2)           A(DAIRFAIL PARAMETER LIST)
         AGO   .FAIL050
.FAIL040 ANOP
&REG     SETC  '&MF(2)'(2,K'&MF(2)-2)
         LR    R1,&REG             A(DAIRFAIL PARAMETER LIST)
.FAIL050 ANOP  ,
         AIF   ('&RB'(1,1) EQ '(').FAIL060
         AIF   ('&RB' NE '').FAIL070
         MNOTE 8,'REQ BLOCK ADDRESS NOT SPECIFIED'
         MEXIT
.FAIL060 ANOP  ,
         ST    &RB(1),DFS99RBP-DFDSECTD(,R1)
         AGO   .FAIL080
.FAIL070 ANOP  ,
         LA    R14,&RB             A(REQUEST BLOCK)
         ST    R14,DFS99RBP-DFDSECTD(,R1)
.FAIL080 ANOP  ,
         AIF   ('&RC'(1,1) EQ '(').FAIL090
         LA    R14,&RC             LOAD ADDRESS OF RETURN CODE
         ST    R14,DFRCP-DFDSECTD(,R1)
         AGO   .FAIL100
.FAIL090 ANOP
         LA    R14,DFLEN(,R1)      A(RETURN CODE AREA)
         ST    &RC(1),0(,R14)      SAVE RETURN CODE
         ST    R14,DFRCP-DFDSECTD(,R1)
.FAIL100 ANOP  ,
         LA    R14,=A(0)           A(FULLWORD OF ZEROS)
         ST    R14,DFJEFF02-DFDSECTD(,R1)
         LA    R14,DFLEN+4(,R1)    A(ID SWITCHES)
         ST    R14,DFIDP-DFDSECTD(,R1)
         AIF   ('&MF(3)' EQ '').FAIL110
         MVI   DFLEN+4(R1),DFBUFSW
         AGO   .FAIL120
.FAIL110 ANOP  ,
         MVI   DFLEN+4(R1),DFWTP
.FAIL120 ANOP  ,
         AIF   ('&TYPE' NE 'ALLOC').FAIL130
         MVI   DFLEN+4+1(R1),DFSVC99
         AGO   .FAIL150
.FAIL130 ANOP  ,
         AIF   ('&TYPE' NE 'FREE').FAIL140
         MVI   DFLEN+4+1(R1),DFFREE
         AGO   .FAIL150
.FAIL140 ANOP  ,
         AIF   ('&TYPE' NE 'DAIR').FAIL150
         MVI   DFLEN+4+1(R1),DFDAIR
.FAIL150 ANOP  ,
         AIF   ('&CPPL' EQ '').FAIL160
         AIF   ('&CPPL'(1,1) EQ '(').FAIL170
         LA    R14,&CPPL           A(CPPL)
         ST    R14,DFCPPLP-DFDSECTD(,R1)
         AGO   .FAIL180
.FAIL160 ANOP  ,
         XC    DFCPPLP-DFDSECTD(,R1),DFCPPLP-DFDSECTD(R1)
         AGO   .FAIL180
.FAIL170 ANOP  ,
         ST    &CPPL(1),DFCPPLP-DFDSECTD(,R1)
.FAIL180 ANOP  ,
         AIF   ('&MF(3)' EQ '').FAIL200
         AIF   ('&MF(3)'(1,1) EQ '(').FAIL190
         LA    R14,&MF(3)
         ST    R14,DFBUFP-DFDSECTD(,R1)
         AGO   .FAIL210
.FAIL190 ANOP  ,
&REG     SETC  '&MF(3)'(2,K'&MF(3)-2)
         ST    &REG,DFBUFP-DFDSECTD(,R1)
         AGO   .FAIL210
.FAIL200 ANOP  ,
         XC    DFBUFP-DFDSECTD(,R1),DFBUFP-DFDSECTD(R1)
.FAIL210 ANOP  ,
         LINK  EP=IKJEFF18
         AIF   ('&MF(1)' NE 'G').EXIT
         SPACE 1
         RCPDS
&LBL     SETC  'FAIL&SYSNDX'
&LBL     DS    0F,XL(DFLEN)   RESERVE SPACE FOR DFDSECTD
         DS    F              RESERVE SPACE FOR RETURN CODE
         DS    XL2            RESERVE SPACE FOR ID SWITCHES
         AIF   ('&MF(3)' EQ '').FAIL220
         DS    XL(DFLEN2)     RESERVE SPACE FOR DFDSECT2
.FAIL220 ANOP  ,
         RCPDS
.EXIT    MEND
.*       S99FAIL ,                     - WTO NON-RENT
.*       S99FAIL MF=(E,PLIST)          - WTO RENT
.*       S99FAIL MF=(E,PLISTA,PLISTB)  - RETURN MSGS RENT
.*       LA    R2,DFLIST2
.*       USING DFDSECT2,R2
.*       ...
.*       DROP  R2
.*PLIST  DS    0F,CL(DFLEN),F,XL2      - STANDARD PARAMETER LIST
.*PLIST2 DS    0F,CL(DFLEN2)           - REPLY BUFFER PARAMETER LIST
