         MACRO
&NAME    SET
.*******************************************************************
.*                                                                 *
.*       MACRO TO SET A SWITCH DECLARED BY DCLSW.                  *
.*       MACRO RCPLOCSW IS USED TO FIND IF THE SWITCH HAS BEEN     *
.*       DECLARED AND TO LOCATE THE SWITCH BYTE WHOSE NAME IS THEN *
.*       RETURNED IN GLOBAL SETC &RCPDSW1. &RCPDSW2 IS SET TO THE  *
.*       SWITCH NAME IF 1 SWITCH IS SPECIFIED, OR TO THE SWITCH    *
.*       NAMES CONCATENATED TO EACH OTHER WITH INTERVENING PLUS    *
.*       SIGNS. IF THE SWITCH WAS NOT DECLARED, &RCPDSW1 IS SET    *
.*       TO NULL BY RCPLOCSW.                                      *
.*                                                                 *
.*******************************************************************
         GBLA  &RCPDSW0                NO OF SWITCH BYTES FOUND
         GBLB  &RCPDSW3(20)
         GBLC  &RCPDSW1(20),&RCPDSW2(20) PARMS RETURNED BY RCPLOCSW
         LCLA  &I,&J,&K,&L
         LCLC  &MASK
         LCLC  &LABEL
&LABEL   SETC  '&NAME'
&I       SETA  1
         AGO   .LOOPS
.LOOP1   AIF   (&I GE N'&SYSLIST).EXIT
&I       SETA  &I+2
.LOOPS   RCPLOCSW                      &SYSLIST(&I)     LOCATE SWITCHES
&J       SETA  0                       CLEAR LOOP COUNTER
&MASK    SETC  '&SYSLIST(&I+1)'           GET MASK
         AIF   ('&MASK' EQ 'ON' OR '&MASK' EQ '').ON
         AIF   ('&MASK' EQ 'OFF').OFF
         AIF   ('&MASK' EQ 'FLIP').FLIP
         MNOTE 4,'INVALID SET OPTION ''&MASK'', ''ON'' ASSUMED'
.ON      AIF   (&J GE &RCPDSW0).LOOP1
&J       SETA  &J+1
         AIF   (&RCPDSW3(&J)).ONOFF    BRANCH IF SWS INVERTED
&LABEL   OI    &RCPDSW1(&J),&RCPDSW2(&J)
&LABEL   SETC  ''                      ONLY WANT LABEL FIRST TIME
         AGO   .ON                     CONTINUE
.ONOFF   ANOP
&LABEL   NI    &RCPDSW1(&J),255-(&RCPDSW2(&J))
&LABEL   SETC  ''                      ONLY WANT LABEL FIRST TIME
         AGO   .ON                     CONTINUE
.OFF     AIF   (&J GE &RCPDSW0).LOOP1
&J       SETA  &J+1
         AIF   (&RCPDSW3(&J)).OFFON    BRANCH IF SWS INVERTED
&LABEL   NI    &RCPDSW1(&J),255-(&RCPDSW2(&J))
&LABEL   SETC  ''                      ONLY WANT LABEL FIRST TIME
         AGO   .OFF                    CONTINUE
.OFFON   ANOP
&LABEL   OI    &RCPDSW1(&J),&RCPDSW2(&J)
         AGO   .OFF                    CONTINUE
.FLIP    AIF   (&J GE &RCPDSW0).LOOP1
&J       SETA  &J+1
&LABEL   XI    &RCPDSW1(&J),&RCPDSW2(&J)
&LABEL   SETC  ''
         AGO   .FLIP
.EXIT    AIF   ('&LABEL' EQ '').END
&LABEL   DS    0H                      IF ERROR OCCURRED, PROVIDE LABEL
.END     MEND
