         MACRO -   TO CHECK FOR MUTUALLY EXCLUSIVE KEYWORDS
         RCPMUEXC &RC=4       ERROR CODE FOR MNOTE
         LCLA  &I,&J
         LCLC  &C
.*
.*   THIS IS AN INNER MACRO WHICH CAN BE USED TO CHECK
.*   FOR MUTUALLY EXCLUSIVE KEYWORDS.
.*   FOR INSTANCE, IF A MACRO CALLED 'M' HAS 3 KEYWORDS,
.*   'A','B','C', EACH OF WHICH IS MUTUALLY EXCLUSIVE,
.*   THAT MACRO COULD CODE
.*
.*   RCPMUEXC (A=,&A),(B=,&B),(C=,&C),RC=N
.*
.*   WHERE N IS THE ERROR CODE FOR AN MNOTE (DEFAULT=4).
.*   THE KEYWORDS ARE SPECIFIED IN HEIRARCHICAL ORDER.
.*   I.E., IF A=XXXX, THE KEYWORDS B= & C= MUST BE NULL.
.*   B=XXX IS ONLY VALID IF A=''  AND C=XXXX IS ONLY VALID IF
.*   B=''. AN MNOTE IS GENERATED FOR EVERY MUTUALLY EXCLUSIVE
.*   KEYWORD FOUND.
&J       SETA  N'&SYSLIST     COUNT OF ENTRIES
.LOOP    ANOP
&I       SETA  &I+1                    GET NEXT POSIT PARAMETER
         AIF   (&I GT &J).EXIT         EXIT IF NO MORE POSITIONALS
         AIF   ('&SYSLIST(&I,2)' EQ '').LOOP IGNORE IF NULL OP
&C       SETC  '&SYSLIST(&I,1)'        SAVE KEYWORD NAME
.LOOP2   ANOP
&I       SETA  &I+1                    GET NEXT POSIT PARAMETER
         AIF   (&I GT &J).EXIT         EXIT IF ALL DONE
         AIF   ('&SYSLIST(&I,2)' EQ '').LOOP2 IGNORE IF NULL OP
         MNOTE &RC,'&SYSLIST(&I,1) MUTUALLY EXCLUSIVE WITH &C, IT IS IGX
               NORED'
         AGO   .LOOP2
.EXIT    MEND
