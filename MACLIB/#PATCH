         MACRO ,
&LABEL   #PATCH &F=5,&ORG=
         GBLA  &#PATCHN
         GBLB  &USINGRELATIVE           RELATIVE BASE
         LCLA  &N
         LCLC  &BEGIN
         LCLC  &TYPE
&#PATCHN SETA  &#PATCHN+1
&N       SETA  &#PATCHN
&BEGIN   SETC  '&SYSECT'
&TYPE    SETC  'S(*)'
         AIF   (NOT &USINGRELATIVE).PAT0100
&TYPE    SETC  'AL2(*-@PATCH&N)'
.PAT0100 ANOP  ,
         AIF   ('&ORG' EQ '').PAT0200
&BEGIN   SETC  '&ORG'
.PAT0200 PUSH  PRINT
         PRINT ON,GEN,DATA
         AIF   ('&F' EQ 'S').PAT0300
@PSIZE&N EQU   ((*-&BEGIN+99)/100)*&F
@PATCH&N DC    28&TYPE                  MODULE PATCH AREA
         ORG   @PATCH&N
&LABEL   DC    ((@PSIZE&N+1)/2)&TYPE
         ORG   ,
         AGO   .MEXIT
.PAT0300 ANOP  ,
         AIF   ('&LABEL' NE '').PAT0400
@PATCH&N DC    28&TYPE                  MODULE PATCH AREA
         AGO   .MEXIT
.PAT0400 ANOP  ,
&LABEL   DC    28&TYPE                  MODULE PATCH AREA
.MEXIT   ANOP  ,
         POP   PRINT
         MEND  ,
