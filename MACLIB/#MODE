         MACRO ,
&LABEL   #MODE &WHAT,&R=R14
         GBLC  &SYSSPLV             SPLEVEL
         SPLEVEL TEST
         AIF   ('&SYSSPLV' NE '1').LEVEL2
         AIF   ('&R' EQ 'R14' OR '&R' EQ '14').LEVEL1
         MNOTE 12,'**** R=&R Invalid - Must Be R14 When SPLEVEL=1'
         MEXIT ,
.LEVEL1  AIF   ('&WHAT' EQ '24').SET124
         AIF   ('&WHAT' EQ '31').SET131
         MNOTE 8,'**** Operand Must Be ''24'' Or ''31'''
         MEXIT ,
.SET124  ANOP  ,
&LABEL   LA    &R,*+8                   POINT AFTER BRANCH
         B     890                      GO THROUGH PSARET
         MEXIT ,
.SET131  ANOP  ,
&LABEL   LA    &R,*+12                  POINT AFTER BRANCH
         O     &R,=X'80000000'          SET AMODE-31 BIT
         B     890                      GO THROUGH PSARET
         MEXIT ,
.LEVEL2  AIF   ('&WHAT' EQ '24').SET224
         AIF   ('&WHAT' EQ '31').SET231
         MNOTE 8,'**** Operand Must Be ''24'' Or ''31'''
         MEXIT ,
.SET224  ANOP  ,
&LABEL   LA    &R,*+6                   POINT AFTER BSM
         BSM   0,&R                     SET ADDRESSING MODE
         MEXIT ,
.SET231  ANOP  ,
&LABEL   LA    &R,*+10                  POINT AFTER BSM
         O     &R,=X'80000000'          SET AMODE-31 BIT
         BSM   0,&R                     SET ADDRESSING MODE
.MEND    MEND  ,
