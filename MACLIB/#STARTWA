         MACRO ,
         #STARTWA &LTORG=YES,&WALLY=MAYBE,&PATCH=
.*
.* DEFINE SYMBOLS NEEDED, NOTE ALL GLOBALS ARE FROM #START MACRO
.*
         GBLA  &EXECUTN                 EXECUTE MACRO COUNTER
         GBLA  &SPFCNT                  ISPLINK VERB COUNT
         GBLA  &STWAC                   COUNTER FOR DEFERED WA DEFINES
         GBLB  &STWA                    INDICATES CSECT HAS A WORK AREA
         GBLB  &STF(24)                 CONTROL FLAG BITS, AS FOLLOWS:
.*                                       (01) - PROC OPTION CODED
.*                                       (02) - MAIN OPTION CODED
.*                                       (03) - LIFO OPTION CODED
.*                                       (04) - WKCSECT OPTION CODED
.*                                       (05) - WKDSECT OPTION CODED
.*                                       (06) - #START HAS BEEN CODED
.*                                       (07) - ROUTINE IS AN SVC
.*                                       (08) - WORK FIELDS WANTED
.*                                       (09) - SVAREA=NO OPTION
.*                                       (10) - SAVE=YES OPTION
.*                                       (11) - RSECT=YES OPTION
.*                                       (12) - SVC=(YES,BRANCH)
.*                                       (13) - ENUC CAPPING
.*                                       (14) - PATCH AREA REQUIRED
.*                                       (18) - EXTKEY= OPTION CODED
.*                                       (22) - 64 BIT REGS
.*                                       (23) - #STACK
.*
         GBLC  &STPREF                  PREFIX CHARACTERS
         GBLC  &STWANAM                 WORK AREA NAME
         GBLC  &STAMODE                 MODULE'S CAPPED AMODE
         GBLC  &STACREG                 CAPWORD SAVE REGISTER
         GBLC  &STISASZ                 LIFO ISA SIZE
         GBLC  &SYSSPLV                 SPLEVEL
         GBLC  &STWAD(1000)             WORK AREA DEFERED STACK
         LCLA  &A,&B,&C,&D,&E           LOCAL
         LCLB  &F,&G,&H,&I,&J           WORK
         LCLC  &K,&L,&M,&N,&O,&P        SYMBOLS
.*
.* INITIALIZE SYMBOLS, SET UP PREFIX, PICK UP ALL OPERANDS
.*
&P       SETC  '&STPREF'                MAKE PREFIX EASIER TO WORK WITH
         AIF   (NOT &STF(6)).ST90100    ERROR - NO #START MACRO CODED
         AIF   (NOT &STWA).ST90200      ERROR - NO WORK AREA SPECIFIED
         AIF   ('&LTORG' NE 'YES').ST00300   NO LTORG, CONTINUE
         SPACE 1
         AIF   (&EXECUTN EQ 0).ST00100       NO POOL, CONTINUE
*        #EXEC ,                        EXECUTE POOL
         #EXEC ,
         SPACE 1 ,
.ST00100 ANOP  ,
         AGO   .ST00200
         AIF   (&SPFCNT EQ 0).ST00200       NO ISPLINK VERBS, CONTINUE
*        ISPLINK ,                      ISPF VERBS
         ISPLINK ,
         SPACE 1 ,
.ST00200 ANOP  ,
         LTORG ,                        LITERAL POOL
         SPACE 1 ,
.ST00300 ANOP  ,
         AIF   ('&PATCH' EQ 'NO').ST00500
         AIF   ('&PATCH' EQ '' AND NOT &STF(14)).ST00500
         AIF   ('&PATCH' EQ '' AND &STF(20)).ST00500
         AIF   ('&PATCH' EQ 'YES' OR '&PATCH' EQ 'Y' OR '&PATCH' EQ '')X
               .ST00400
         SPACE 1
         #PATCH F=&PATCH                EXPLICIT PATCH SIZE
         AGO   .ST00500
.ST00400 ANOP  ,
         #PATCH ,                       DEFAULT PATCH SIZE
.ST00500 ANOP  ,
.*
.* THE FOLLOWING IS FOR WKCSECT OPTION
.*
         AIF   (NOT &STF(4)).ST00600    NOT WORK CSECT, CONTINUE
&STWANAM CSECT ,                        #STARTWA MACRO
         DS    18F                      SAVE AREA
         AGO   .ST00900                 CONTINUE
.*
.* THE FOLLOWING IS FOR WKDSECT OPTION, WITH OR WITHOUT PROC AND LIFO
.* OPTIONS, BUT NOT WITH THE MAIN OPTION
.*
.ST00600 ANOP  ,
         AIF   (&STF(2)).ST00700        MAIN OPTION, CONTINUE
&STWANAM DSECT ,                        #STARTWA MACRO
         AIF   (&STF(23)).ST00650       NO SAVE AREA WITH BAKR
         DS    18F                      SAVE AREA
         AIF   (NOT &STF(22)).ST00650   NOT 64 BIT REGS
&P.HISAVE DS   13F                      SAVE HIGH WORDS R0-R12
.ST00650 ANOP  ,
         AGO   .ST00900                 CONTINUE
.*
.* THE FOLLOWING IS FOR PROC AND MAIN OPTIONS, BUT NOT LIFO
.*
.ST00700 ANOP  ,
         AIF   (&STF(3)).ST00800        LIFO OPTION, CONTINUE
&STWANAM DSECT ,                        #STARTWA MACRO
         DS    18F                      STANDARD SAVE AREA
&P.MCA   DS    0F                       MODULE COMMUNICATIONS AREA
&P.#O    DS    A                        ORIGIN OF GETMAINED AREA
         AGO   .ST00900                 CONTINUE
.*
.* THE FOLLOWING IS FOR PROC, MAIN, AND LIFO OPTIONS
.*
.ST00800 ANOP  ,
&STWANAM DSECT ,                        #STARTWA MACRO
         DS    18F                      STANDARD SAVE AREA
&P.MCA   DS    0F                       MODULE COMMUNICATIONS AREA
&P.#O    DS    A                        ORIGIN OF GETMAINED AREA
&P.#GET  DS    V(#####GET)              PTR TO LIFO GET ROUTINE
&P.#FRE  DS    V(####FREE)              PTR TO LIFO FREE ROUTINE
&P.#S    DS    A                        PTR TO START OF LIFO STACK
&P.#E    DS    A                        PTR TO END OF LIFO STACK
&P.#N    DS    A                        PTR TO NEXT FREE AREA
.*
.* THE FOLLOWING IS COMMON TO ALL TYPES OF WORK AREAS
.*
.ST00900 ANOP  ,
         AIF   ('&WALLY' EQ 'NO').ST01000
         AIF   (NOT &STF(8) AND '&WALLY' NE 'YES').ST01000
&P.DBL1  DS    D
&P.DBL2  DS    D
&P.DBL3  DS    D
&P.DBL4  DS    D
&P.WORD1 DS    F
&P.WORD2 DS    F
&P.WORD3 DS    F
&P.WORD4 DS    F
&P.HALF1 DS    H
&P.HALF2 DS    H
&P.HALF3 DS    H
&P.HALF4 DS    H
&P.BYTE1 DS    C
&P.BYTE2 DS    C
&P.BYTE3 DS    C
&P.BYTE4 DS    C
&P.FLAG1 DS    X
&P.FLAG2 DS    X
&P.FLAG3 DS    X
&P.FLAG4 DS    X
.ST01000 ANOP  ,
&STF(8)  SETB  0                        TURN BACK OFF
         AIF   (&STF(23)).ST01050       NO RCODE IF #STACK
&P.RCODE DS    F                        RETURN CODE FIELD
.ST01050 ANOP  ,
         AIF   ('&STAMODE' EQ '' OR '&STACREG' NE '').ST01100
&P.CAPWD DS    A                        USED BY CAPPING SUPPORT
.ST01100 ANOP  ,
         AIF   (NOT &STF(18)).ST01200
&STF(18) SETB  0                        TURN BACK OFF
&P.IPK   DS    A                        INITIAL PROTECT KEY
.ST01200 ANOP  ,
&A       SETA  &STWAC                   PICK UP # DEFERRED WORK FIELDS
&B       SETA  -1                       SET UP INDEX COUNTER
.ST01300 ANOP  ,
&B       SETA  &B+2                     POINT TO NEXT ELEMENT ON STACK
         AIF   (&B GE &A).ST01800       NO MORE, CONTINUE
&K       SETC  '&STWAD(&B)'             PICK UP LABEL
&L       SETC  '&STWAD(&B+1)'           PICK UP OPERAND
&C       SETA  K'&L                     PICK UP LENGTH OF OPERAND
         AIF   ('&L'(&C,1) EQ '''').ST01600   THIS IS A DC
         AIF   ('&L'(&C,1) NE ')').ST01700    THIS IS A DS
.ST01400 ANOP  ,
&C       SETA  &C-1                     BY SCANNING BACKWARDS
         AIF   (&C LT 2).ST01700        ERROR - NO LEFT PARENTHESIS
         AIF   ('&L'(&C,1) EQ '(').ST01500   FOUND THE LEFT
         AGO   .ST01400                 GO CONTINUE SCAN
.ST01500 ANOP  ,
         AIF   ('&L'(&C-1,1) EQ 'L').ST01700   ITS A DS WITH LENGTH
.ST01600 ANOP  ,
&K       DC    &L                       DEFERRED WORK FIELD
         AGO   .ST01300                 GO GET NEXT ELEMENT ON STACK
.ST01700 ANOP  ,
&K       DS    &L                       DEFERRED WORK FIELD
         AGO   .ST01300                 GO GET NEXT ELEMENT ON STACK
.ST01800 ANOP  ,
         MEXIT ,                        EXIT
.ST90100 MNOTE 4,'ST3001A - #START MACRO NOT CODED IN THIS PROGRAM'
         MEXIT ,
.ST90200 MNOTE 4,'ST3002A - #START MACRO NOT DEFINED WITH WORKAREA'
.MEND    MEND  ,
