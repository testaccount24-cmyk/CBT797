.*********************************************************************.
.*                                                                   *.
.*  USED WITH #IPROC,  #ICALL, #IPEND.                               *.
.*                                                                   *.
.*                                                                   *.
.*        #ICALL  CALLS THE SPECIFIED INTERNAL SUBROUTINE            *.
.*                                                                   *.
.*********************************************************************.
         MACRO
&NAME    #ICALL &RTN,&LINKREG=R14,&ERROR=,&AMODE=
         GBLB  &STF(24)            FLAGS FROM #START
         LCLC  &REG
         AIF   ('&RTN'(1,1) EQ '(').REGFORM
         AIF   (&STF(19)).RBR      RELATIVE BRANCHES?
&NAME    L     &LINKREG,=A(&RTN)   PICK UP ADDRESS OF SUB-ROUTINE
         AGO   .AMODE
.RBR     ANOP  ,
&NAME    LARL  &LINKREG,&RTN       LOAD ROUTINE ADDRESS
.AMODE   AIF   ('&AMODE' EQ '31').AM31
         AIF   ('&AMODE' EQ '64').AM64
         BASR  &LINKREG,&LINKREG   BASR TO SUB-ROUTINE
         AGO   .ERRCHK
.AM31    OILH  &LINKREG,X'8000'    PASS CONTROL IN 31 BIT MODE
         AGO   .BASSM
.AM64    OILL  &LINKREG,X'0001'    PASS CONTROL IN 64 BIT MODE
.BASSM   BASSM &LINKREG,&LINKREG   BASSM TO SUB-ROUTINE
         AGO   .ERRCHK
.REGFORM ANOP
&REG     SETC  '&RTN'(2,K'&RTN-2)
         AIF   ('&AMODE' EQ '31').RAM31
         AIF   ('&AMODE' EQ '64').RAM64
&NAME    BASR  &LINKREG,&REG       BASR TO SUB-ROUTINE
         AGO   .ERRCHK
.RAM31   ANOP  ,
&NAME    OILH  &REG,X'8000'        PASS CONTROL IN 31 BIT MODE
         BASSM &LINKREG,&REG       BASSM TO SUB-ROUTINE
         AGO   .ERRCHK
.RAM64   ANOP  ,
&NAME    LA    &REG,1(,&REG)       PASS CONTROL IN 64 BIT MODE
         BASSM &LINKREG,&REG       BASSM TO SUB-ROUTINE
.ERRCHK  AIF   (T'&ERROR EQ 'O').NERR
         LTR   R15,R15             WAS THERE AN ERROR?
         AIF   (&STF(19)).RBR002   RELATIVE BRANCHES?
         BNZ   &ERROR              YES - GO TO ERROR ROUTINE
         AGO   .NERR
.RBR002  JNZ   &ERROR              YES - GOTO ERROR ROUTINE
.NERR    ANOP  ,
         MEXIT ,
         MEND  ,
