         MACRO ,
&NAME    #CON  &A,                      #CON                           X
               &PATCH=YES,                                             X
               &F=5,                                                   Z
               &ORG=,                                                  x
               &LOAD=YES,                                              x
               &PREFIX=,                                               x
               &BASE=                   ADDRESSING MODE
.*--------------------------------------------------------------------*
.*                                                                    *
.*       #CON MACRO:                                                  *
.*                                                                    *
.*        #CON RESUME - CHANGE FROM THE CODE CSECT TO THE CONSTANT    *
.*                                                                    *
.*        #CON REVERT - BACK TO THE BASE CSECT                        *
.*                                                                    *
.*        #CON START - DEFINES THE START OF THE CONSTANT SECTION      *
.*              PATCH=NO - NO PATCH AREA                              *
.*              PATCH=YES - STANDARD LENGTH PATCH AREA                *
.*              PATCH=XX - LENGTH XX PATCH                            *
.*                                                                    *
.*        #CON END   - THE END OF  THE CONSTANT AREA                  *
.*                                                                    *
.*        #CON USING - RESET ADDRESSABILITY TO THE CONSTANT AREA      *
.*              BASE = RX - SET THE BASE TO RX - OMIT TO GET THE      *
.*                     DEFAULT BASE = BASE FROM #START                *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &STA_CONSTANT_SECTION    NAME OF CONSTANT SECT
         GBLC  &STA_CONSTANT_BASE       BASE OF CONSTANT SECT
         GBLC  &CONS_SECT,&CONS_STYP
         GBLC  &STA_CONSTANT_LABEL
         GBLA  &#PATCHN            Patch
         GBLB  &STA_CONST,&SMODE   Have generated constants
         LCLA  &N
         LCLC  &BEGIN
         LCLC  &CSNAME
         AIF   ('&A' NE 'USING').MORE
         AIF   (T'&BASE NE 'O').SETBASE
&STA_CONSTANT_LABEL USING &STA_CONSTANT_SECTION,&STA_CONSTANT_BASE
         AIF   ('&LOAD' EQ 'NO').SKLOAD
         #RBL  &STA_CONSTANT_BASE,&STA_CONSTANT_SECTION
.SKLOAD  ANOP  ,
         MEXIT ,
.SETBASE ANOP  ,
&STA_CONSTANT_LABEL USING &STA_CONSTANT_SECTION,&BASE
         #RBL  &BASE,&STA_CONSTANT_SECTION
         MEXIT ,
.MORE    ANOP  ,
         AIF   ('&A' EQ '').ERR
         AIF   ('&A' EQ 'START').START
         AIF   ('&A' EQ '#START').STARTAA  From within #START
         AIF   ('&A' EQ 'RESUME').RESUME
         AIF   ('&A' EQ 'REVERT').REVERT
         AIF   ('&A' EQ 'END').END
         AIF   ('&A' EQ 'DROP').DROP
.ERR     MNOTE 8,'OPERANDS IN ERROR'
.STARTAA ANOP  ,
.START    ANOP ,
&CONS_SECT SETC '&SYSECT'
&CONS_STYP SETC '&SYSSTYP'
&STA_CONSTANT_SECTION LOCTR ,
         AIF   (&STA_CONST).GCONST Already have constants
         DC    CL8'&CONS_SECT'     Module name
         DC    A(&CONS_SECT)       .and address
&STA_CONST SETB 1
.GCONST  ANOP  ,
         AIF   ('&A' EQ '#START').REVERT From within #START
         MEXIT ,
.RESUME  ANOP  ,
&CONS_SECT SETC '&SYSECT'
&CONS_STYP SETC '&SYSSTYP'
&CONS_STYP SETC 'LOCTR'
&CSNAME  SETC  '&PREFIX&STA_CONSTANT_SECTION'
&CSNAME  SETC  '&CSNAME'(1,8)
&CSNAME  LOCTR ,
         AIF   ('&PREFIX' EQ '').SKPREF
         AIF   (&SMODE).SKPREF
&SMODE   SETB  1
*&CSNAME AMODE 31
*&CSNAME RMODE ANY
.SKPREF  ANOP  ,
         MEXIT ,
.END     ANOP  ,
         LTORG ,
&CONS_SECT &CONS_STYP ,  RESUME INTIAL SECTION
&CONS_SECT LOCTR ,  RESUME INTIAL SECTION
         AIF   ('&PATCH' EQ 'NO').ST00500
&#PATCHN SETA  &#PATCHN+1
&N       SETA  &#PATCHN
&BEGIN   SETC  '&CONS_SECT'
         AIF   ('&ORG' EQ '').Z
&BEGIN   SETC  '&ORG'
.Z       PUSH  PRINT
         PRINT ON,GEN,DATA
         AIF   ('&F' EQ 'S').FIXED
@PSIZE&N EQU   ((*-&BEGIN+99)/100)*&F
&STA_CONSTANT_SECTION LOCTR ,
@PATCH&N DC    25S(*)                   MODULE PATCH AREA
         ORG   @PATCH&N
         DC    ((@PSIZE&N+1)/2)S(*)
         ORG   ,
&CONS_SECT &CONS_STYP ,  RESUME INTIAL SECTION
&CONS_SECT LOCTR ,  RESUME INTIAL SECTION
         POP   PRINT
         MEXIT ,
.FIXED   ANOP  ,
&STA_CONSTANT_SECTION LOCTR ,
         DC    28S(*)                   MODULE PATCH AREA
&CONS_SECT &CONS_STYP ,  RESUME INTIAL SECTION
&CONS_SECT LOCTR ,  RESUME INTIAL SECTION
         POP   PRINT
.ST00500 ANOP  ,
         MEXIT ,
.REVERT  ANOP  ,
&CONS_SECT &CONS_STYP ,  RESUME INTIAL SECTION
&CONS_SECT LOCTR ,  RESUME INTIAL SECTION
         MEXIT ,
.DROP    ANOP  ,
         DROP  C
.MEND    MEND  ,
