         MACRO ,
         #STOPWA ,                      #STOPWA MACRO
.*
.* DEFINE SYMBOLS NEEDED, NOTE ALL GLOBALS ARE FROM #START MACRO
.*
         GBLA  &STWAC                   COUNTER FOR DEFERED WA DEFINES
         GBLB  &STWA                    INDICATES CSECT HAS A WORK AREA
         GBLB  &STF(24)                 CONTROL FLAG BITS, AS FOLLOWS:
.*                                       (01) - PROC OPTION CODED
.*                                       (02) - MAIN OPTION CODED
.*                                       (03) - LIFO OPTION CODED
.*                                       (04) - WKCSECT OPTION CODED
.*                                       (05) - WKDSECT OPTION CODED
.*                                       (06) - #START HAS BEEN CODED
.*                                       (07) - ROUTINE IS AN SVC
.*                                       (08) - WORK FIELDS WANTED
.*                                       (09) - SVAREA=NO OPTION
.*                                       (10) - SAVE=YES OPTION
.*                                       (11) - RSECT=YES OPTION
.*                                       (12) - SVC=(YES,BRANCH)
.*                                       (13) - ENUC CAPPING
.*                                       (14) - PATCH AREA REQUEST
.*                                       (18) - EXTKEY= OPTION
.*
         GBLC  &STPREF                  PREFIX CHARACTERS
         GBLC  &STWANAM                 WORK AREA NAME
         GBLC  &STNAME                  MAIN CSECT NAME
         GBLC  &STAMODE                 MODULE'S CAPPED AMODE
         GBLC  &STACREG                 CAPWORD SAVE REGISTER
         GBLC  &STISASZ                 LIFO ISA SIZE
         GBLC  &SYSSPLV                 SPLEVEL
         GBLC  &STA_CONSTANT_SECTION
         GBLC  &STWAD(1000)             WORK AREA DEFERED STACK
         LCLA  &A,&B,&C,&D,&E           LOCAL
         LCLB  &F,&G,&H,&I,&J           WORK
         LCLC  &K,&L,&M,&N,&O,&P        SYMBOLS
         LCLC  &CSNAME                  SAVE CSECT NAME
.*
.* INITIALIZE SYMBOLS, SET UP PREFIX
.*
&P       SETC  '&STPREF'                MAKE PREFIX EASIER TO WORK WITH
         AIF   (NOT &STF(6)).ST90100    ERROR - NO #START MACRO CODED
         AIF   (NOT &STWA).ST90200      ERROR - NO WORK AREA SPECIFIED
.*
.* THE FOLLOWING IS COMMON TO ALL WORKAREAS
.*
         DS    0D                       FORCE WORK AREA TO DWD BOUNDARY
&P.WALEN EQU   *-&STWANAM               LENGTH OF WORK AREA
.*
.* THE FOLLOWING IS FOR THE PROC OPTION ONLY
.*
         AIF   (NOT &STF(1)).ST00200    NO PROC OPTION, CONTINUE
         AIF   (&STF(2)).ST00200        MAIN OPTION, CONTINUE
         AIF   (&STF(3)).ST00100        LIFO OPTION, CONTINUE
         AIF   (&STF(23)).ST00200       #STACK W/O LIFO
         SPACE 1
&P.MCA   DSECT ,                        MODULE COMMUNICATIONS AREA
&P.#O    DS    A                        ORIGIN OF GETMAINED AREA
         AGO   .ST00200                 CONTINUE
.*
.* THE FOLLOWING IS FOR PROC AND LIFO OPTIONS, BUT NOT MAIN
.*
.ST00100 ANOP  ,
         SPACE 1
&P.MCA   DSECT ,                        MODULE COMMUNICATIONS AREA
&P.#O    DS    A                        ORIGIN OF GETMAINED AREA
&P.#GET  DS    V(#####GET)              PTR TO LIFO GET ROUTINE
&P.#FRE  DS    V(####FREE)              PTR TO LIFO FREE ROUTINE
&P.#S    DS    A                        PTR TO START OF LIFO STACK
&P.#E    DS    A                        PTR TO END OF LIFO STACK
&P.#N    DS    A                        PTR TO NEXT FREE AREA
.*
.* THE FOLLOWING IS COMMON FOR ALL WORK AREA TYPES
.*
.ST00200 ANOP  ,
&CSNAME  SETC  '&STNAME'
         AIF   (&STF(11)).ST00300
&CSNAME  CSECT ,                        RESUME MAIN PROGRAM CSECT
         AGO   .ST00400
.ST00300 ANOP  ,
&CSNAME  RSECT ,                        RESUME MAIN PROGRAM RSECT
.ST00400 ANOP  ,
         AIF   (NOT &STF(2)).ST00600    NOT PROC(MAIN), CONTINUE
         AIF   (&STF(3)).ST00500        SKIP CXD IF LIFO
&P.GSIZE CXD   ,                        GET CUMULATIVE WORK AREA LENGTH
         AGO   .ST00600
.ST00500 ANOP  ,
&P.GSIZE DC    A(&STISASZ)              ISA SIZE REQUESTED
.ST00600 ANOP  ,
         AIF   (NOT &STF(1)).ST00700    NOT PROC OPTION, CONTINUE
         AIF   (&STF(3)).ST00700        SKIP IF LIFO
         AIF   (&STF(23)).ST00700       SKIP IF #STACK
&P.QCON  DC    Q(&STWANAM)              OFFSET TO EXTNL DUMMY SECTION
.ST00700 ANOP  ,
         SPACE 1
         MEXIT ,
.ST90100 MNOTE 4,'ST4001A - #START MACRO NOT CODED IN THIS PROGRAM'
         MEXIT ,
.ST90200 MNOTE 4,'ST4002A - #START MACRO NOT DEFINED WITH WORKAREA'
.MEND    MEND  ,
