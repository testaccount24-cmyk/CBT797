         MACRO ,
&NAME    #SIFT &VEC,&REG,&VER=YES,&ERR=,&WORK=R0
         LCLA  &I,&J
         LCLB  &B
         LCLC  &R,&ERRLBL
&ERRLBL  SETC  'SIF&SYSNDX.E'
&R       SETC  'R15'                    SET DEFAULT INDEX REG
&B       SETB  ('&VEC(N'&VEC)' EQ '*')  IF NOOP AS LAST ELEMENT
         AIF   ('&REG' EQ '').NE
&R       SETC  '&REG'                   OR OVERRIDE REG
.*
.NE      AIF   ('&VER' EQ 'YES').VERIFY
*   CODE OPTIMIZED FOR VER=NO
&NAME    B     *+4(&R)                  ENTER BRANCH TABLE
         AGO   .SIFT
.VERIFY  ANOP  ,
         AIF   (N'&VEC GT 4).BRTAB
.XPLICIT ANOP  ,
&I       SETA  0
&NAME    LTR   &R,&R                    CHECK IF ZERO
.CHLOOP  AIF   (&I GE N'&VEC).CHEND
&J       SETA  &I*4
&I       SETA  &I+1
         AIF   (&I EQ 1).BIF
         C     &R,=F'&J'                CHECK CODE
.BIF     AIF   ('&VEC(&I)' EQ '').ERRBX
         AIF   ('&VEC(&I)' EQ '*').ENDBRX
         BE    &VEC(&I)                 BR IF EQUAL
         AGO   .CHLOOP
.ERRBX   BE    &ERRLBL                  TO ERROR IF EQUAL
         AGO   .CHLOOP
.ENDBRX  ANOP  ,
         BE    &ERRLBL+4                TO NSI AFTER MACRO
         AGO   .CHLOOP
.CHEND   ANOP  ,
         AIF   ('&ERR' EQ '').EXAST1
&ERRLBL  B     &ERR                     ERROR BRANCH
         MEXIT ,
.*
.EXAST1  ANOP  ,
&ERRLBL  EX    0,*                      ERROR  (ABEND 0C3)
         MEXIT ,
.*
.BRTAB   ANOP
&NAME    LCR   &WORK,&R
&I       SETA  1                        BYPASS ZERO CASE
         AIF   ('&VEC(1)' EQ '').ERRB
         AIF   ('&VEC(1)' EQ '*').ENDBR
         BZ    &VEC(1)                  BRANCH IF ZERO CODE
         AGO   .MORECHK
.ERRB    BZ    &ERRLBL                  BRANCH IF ZERO CODE ERROR
         AGO   .MORECHK
.ENDBR   ANOP  ,
&J       SETA  N'&VEC*4-4*&B
         BZ    &ERRLBL+&J               BRANCH IF ZERO CODE
.MORECHK NR    &WORK,&R                 CHECK CODE
         SRA   &WORK,2                  MULTIPLE OF 4?
         BNP   &ERRLBL                  NO, ERROR
&J       SETA  4*(N'&VEC-1)             CONSTANT GOOD INDEX RANGE
         CL    &R,=F'&J'                OVER MAX?
         BNH   &ERRLBL.(&R)             ENTER BRANCH TABLE IF GOOD
         AIF   ('&ERR' EQ '').BLOW
&ERRLBL  B     &ERR                     ELSE GO TO ERROR
         AGO   .SIFT
.*
.BLOW    ANOP  ,                        VER=YES, NO ERR EXIT
&ERRLBL  EX    0,*                      ELSE BLOW UP  (ABEND 0C3)
.*
.SIFT    ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT N'&VEC).MEND
         AIF   ('&VEC(&I)' EQ '').EXAST NULL ARG
         AIF   ('&VEC(&I)' EQ '*').NOP  '*' -- GEN NO-OP
         B     &VEC(&I)                  *
         AGO   .SIFT
.*
.EXAST   AIF   ('&ERR' EQ '' AND '&VER' EQ 'NO').EX0C3
         B     &ERRLBL                   *  INVALID CASE
         AGO   .SIFT
.EX0C3   EX    0,*                       *  INVALID CASE
         AGO   .SIFT
.*
.NOP     AIF   (&I EQ N'&VEC).MEND       '*' TABLE ENTRY
&J       SETA  4*(N'&VEC-&I+1)-4*&B
         B     *+&J                      *  TO NSI AFTER MACRO
         AGO   .SIFT                    CLOSE LOOP
.*
.MEND    MEND  ,
