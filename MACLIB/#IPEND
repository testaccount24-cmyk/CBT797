.*********************************************************************.
.*                                                                   *.
.*  USED WITH #IPROC, #ICALL, #IPEND.                                *.
.*                                                                   *.
.*                                                                   *.
.*        IPEND RESTORES REGISTERS FROM THE STACK AND RETURNS        *.
.*                USING 'PR'                                         *.
.*                                                                   *.
.*   RESTR0R1=YES - RESTORES ENTRY R0,R1 VALUES ON RETURN            *.
.*   RESTR15=YES - RESTORES R15 ON RETURN                            *.
.*                                                                   *.
.*********************************************************************.
         MACRO ,
&NAME    #IPEND &RESTR0R1=,&RESTR15=
         GBLB  &STF(24)
         GBLA  &IPROC#             UNIQUE ID OF PREVIOUS IPEND
         GBLC  &STNAME             MAIN CSECT NAME
         LCLB  &R0R1
         LCLB  &R15
&R0R1    SETB  ('&RESTR0R1' EQ 'YES' OR '&RESTR0R1' EQ 'Y')
&R15     SETB  ('&RESTR15' EQ 'YES' OR '&RESTR15' EQ 'Y')
         AIF   (&R0R1 AND &R15).RBOTH
         AIF   (&R0R1).RR0R1
         AIF   (&R15).RR15
&NAME    PR    ,                   RESTORE GPR/AR 2-14 AND RETURN
         AGO   .CDONE
.RBOTH   ANOP  ,
&NAME    EREGG 15,1                RESTORE GPR/AR 15-0
         AGO   .CONT
.RR0R1   ANOP  ,
&NAME    EREGG 0,1                 RESTORE GPR/AR 0-1
         AGO   .CONT
.RR15    ANOP  ,
&NAME    EREGG 15,15               RESTORE GPR/AR 15
.CONT    ANOP  ,
         PR    ,                   RESTORE GPR/AR 2-14
.CDONE   ANOP  ,
         AIF   (&STF(19)).RELADD
         #EXEC ,                   GENERATE ANY EX TARGETS
         LTORG ,                   GENERATE LOCAL LITERAL POOL
IPEND&IPROC# DS 0H                 FORCE ALIGNMENT
.RELADD  ANOP  ,
         POP   USING               RESTORE USING STATUS
&STNAME  LOCTR ,                   RESTORE MAIN CSECT
         MEND  ,
