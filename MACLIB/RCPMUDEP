         MACRO -      TO CHECK FOR MUTUALLY DEPENDENT KEYWORDS
         RCPMUDEP &A,&RC=4        MNOTE ERROR CODE
         LCLA  &I,&J
.*
.*     THIS IS AN INNER MACRO THAT CAN BE USED TO CHECK
.*     FOR KEYWORDS THAT ARE DEPENDENT ON OTHER KEYWORDS.
.*     FOR EXAMPLE IN THE FOLLOWING MACRO:
.*
.*     MACRO
.*     #FRED  &A=,&B=,&C=
.*     ....
.*     ....
.*     MEND
.*
.*     IF C=XXX IS ONLY VALID WHEN &B IS SPECIFIED, I.E. IF
.*     SPECIFICATION OF &C IS ONLY VALID WHEN &B IS SPECIFIED,
.*     #FRED SHOULD INCLUDE THE FOLLOWING.
.*
.*     RCPMUDEP (B=,&B),(C=,&C),RC=NN
.*
.*     WHERE NN IS AN MNOTE ERROR CODE.
.*     IF &B IS NULL, AN MNOTE WILL BE GENERATED FOR
.*     &C IF &C IS SPECIFIED. IF &B IS SPECIFIED OR &C IS
.*     NULL, NO MNOTE WILL BE GENERATED.
.*     ANY NUMBER OF KEYWORDS CAN BE SPECIFIED.
.*     THE FIRST OPERAND OF A SUBLIST IS THE KEYWORD NAME,
.*     AND THE SECOND OPERAND IS THE KEYWORD VALUE.
.*
         AIF   ('&A(2)' NE '').EXIT    OK IF FIRST KW ENTERED
&I       SETA  1
&J       SETA  N'&SYSLIST
.LOOP    ANOP
&I       SETA  &I+1                    GET NEXT POSITIONAL
         AIF   (&I GT &J).EXIT         EXIT IF ALL DONE
         AIF   ('&SYSLIST(&I,2)' EQ '').LOOP   SKIP IF KEYWORD IS NULL
         MNOTE &RC,'**** &SYSLIST(&I,1) CAN ONLY BE SPECIFIED WITH &A(1X
               ), IT IS IGNORED'
         AGO   .LOOP
.EXIT    MEND
