.*********************************************************************.
.*                                                                   *.
.*  USED WITH #IPROC, #ICALL, #IPEND                                 *.
.*                                                                   *.
.*        IPROC allows subroutines to be used in a #START enviro     *.
.*          General and access registers are saved in the stack      *.
.*           using the BAKR instruction.                             *.
.*                                                                   *.
.*        MODE=DEFINE - sets up small work areas                     *.
.*        WA=Y        - This subroutine has its own WA               *.
.*                                                                   *.
.*********************************************************************.
         MACRO ,
&NAME    #IPROC &MODE=,&BASEREG=R10,&LINKREG=R14,&WA=N,&BASE2=,        +
               &ZEROWA=Y,&ARZERO=Y,&MAINWA=N
         GBLA  &IPNUMWA            Number of workareas
         GBLA  &IPNUMWD            Number of workareas declared
         GBLB  &STF(24)
         GBLC  &STWANAM
         LCLA  &WAN
         AIF   ('&MODE' EQ '').IPRO050
         AIF   ('&WA' EQ 'N').IPRO005  "&WA" SPECIFIED
         MNOTE 4,'WA IGNORED WHEN MODE IS SPECIFIED'
.IPRO005 ANOP  ,
         AIF   ('&MODE' EQ 'DEFINE').IPRO030
         MNOTE 12,'INVALID VALUE SPECIFIED FOR MODE - "&MODE"'
         MEXIT ,
.IPRO030 ANOP  ,
IPROCARZ DS    9F                  ZEROS TO INIT ACC REGS
         AIF   (&IPNUMWA EQ 0).IPRO048
&WAN     SETA  1
.IPRO042 ANOP  ,
         DS    0D
IPRWA&WAN DS   XL(IPRWAD&WAN.L)
&WAN     SETA  &WAN+1
         AIF   (&WAN LE &IPNUMWA).IPRO042
.IPRO048 ANOP  ,
         MEXIT ,
.IPRO050 ANOP  ,
         GBLA  &IPROC#             SYSIDX of this IPROC
         GBLC  &STPREF             MODULE PREFIX
         LCLC  &SUBRNAM            SUBROUTINE LABEL
         LCLC  &BR                 BASE REGISTER TO USE
         LCLC  &LR                 LINK REGISTER TO USE
         LCLC  &BRGS               EXPRESSION FOR USING
&IPROC#  SETA  &SYSNDX             SET NUMBER FOR THIS IPROC
&BR      SETC  '&BASEREG'          SET BASE REGISTER TO USE
&LR      SETC  '&LINKREG'          SET LINK REGISTER TO USE
&BRGS    SETC  '&BR'
         AIF   ('&MAINWA' NE 'Y').Skipmwa
         USING &STWANAM,R13     Main work area
.Skipmwa anop  ,
         AIF   ('&BASE2' EQ '').IPRO055
&BRGS    SETC  '&BRGS'.',&BASE2'
.IPRO055 ANOP  ,
&SUBRNAM SETC  '&STPREF.SUBRT'
&SUBRNAM LOCTR ,
         PUSH  USING               SAVE USING STATUS
&NAME    BAKR  &LR,0               SAVE GPRs & ARs
         AIF   ('&ARZERO' NE 'Y').IPRO057
         LAM   AR2,AR10,IPROCARZ   CLEAR THE ACCESS REGISTERS
.IPRO057 AIF   (&STF(19)).RELADD   Relative addressing?
         BALR  &BR,0               OBTAIN LOCAL
         USING (*,IPEND&IPROC#),&BRGS ADDRESSABILITY
         LAE   &BR,0(&BR,0)        ENSURE CLEAN BASE AND AR
         AIF   ('&BASE2' EQ '').IPRO060
         LAE   &BASE2,2048(,&BR)   SET UP
         LA    &BASE2,2048(,&BASE2)  SECOND BASE
.IPRO060 ANOP  ,
.RELADD  ANOP  ,
         AIF   (&IPNUMWA EQ &IPNUMWD).IPRO065
         MNOTE 12,'PREVIOUS IPROC WA NOT DECLARED WITH #IWA START'
.IPRO065 ANOP  ,
         AIF   ('&WA' EQ 'N').IPRO070
&IPNUMWA SETA  &IPNUMWA+1
         USING IPRWAD&IPNUMWA,IPRWA&IPNUMWA
         AIF   ('&ZEROWA' NE 'Y').IPRO070
         XC    IPRWA&IPNUMWA,IPRWA&IPNUMWA  CLEAR WORKAREA
.IPRO070 ANOP  ,
         MEND  ,
