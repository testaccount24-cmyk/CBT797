         MACRO ,
&LABEL   #BAKR &APARS=5,                RESERVE 5 APAR SLOTS           +
               &LEVEL=,                 MODULE LEVEL                   +
               &NAME=                   MODULE NAME (DEFAULT TO MODULE)
         LCLA  &A
         LCLC  &N
         AIF   ('&NAME' EQ '').BK10
&N       SETC  '&NAME'
         AGO   .BK30
.BK10    ANOP  ,
&N       SETC  '&SYSIN_MEMBER'
         AIF   ('&N' NE '').BK30
&N       SETC  '&SYSECT'
.BK30    ANOP  ,
&A       SETA  8+8+9                    ALLOW FOR NAME PLUS DATE/TIME
         AIF   ('&APARS' EQ '').BK40
&A       SETA  &A+(&APARS*4)            ALLOW FOR APARS
.BK40    ANOP  ,
         AIF   ('&LEVEL' EQ '').BK50
&A       SETA  &A+8                     ALLOW FOR LEVEL ID
.BK50    ANOP ,
&LABEL   B     &A+5(0,R15)              BRANCH AROUND ID
         DC    AL1(&A)                  LENGTH OF PROGRAM ID
         DC    CL8'&N'                  MODULE NAME
         DC    CL8' &SYSTIME '          TIME OF ASSEMBLY
         DC    CL9'&SYSDATC'            DATE OF ASSEMBLY
         AIF   ('&LEVEL' EQ '').BK60
         DC    CL8'&LEVEL'              MODULE LEVEL
.BK60    ANOP  ,
         AIF   ('&APARS' EQ '').BK70
         DC    &APARS.CL4' '            APAR SLOTS
.BK70    ANOP  ,
         BAKR  R14,0                    SAVE REGS ON STACK AND SET
*                                        RETURN TO ADDRESS IN R14
         MEND
