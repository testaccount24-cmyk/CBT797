         MACRO ,
&NAME    #SET  &Z
         GBLA  &DCLDSW#             COUNTER OF # OF SWITCH BYTES
         GBLA  &DCLGSW#             COUNTER OF # OF GENERIC BYTES
         GBLB  &DCLDSWD(99)         DEFER DECLARE INDICATORS
         GBLC  &DCLDSWN(99)         SWITCH BYTE NAMES
         GBLC  &DCLDSWB(800)        SWITCH BIT NAMES
         GBLC  &DCLGSWN(99)         GENERIC SWITCH BYTE NAMES
         GBLC  &DCLGSWB(99)         GENERIC SWITCH BIT PREFIXES
         LCLA  &A,&B,&C,&D,&E       LOCAL WORK SYMBOLS
         LCLB  &F(20),&G
         LCLC  &K(20),&L(20),&M,&N,&O,&P,&Q,&SW(20),&MK(20),&MSK(20)
         AIF   (N'&SYSLIST EQ 0).SET2900   ERROR - NO SWITCHES
.*
.* FIRST ISOLATE ALL SWITCHES
.*
&O       SETC  '&NAME'              BUT FIRST GRAB THE LABEL
         AIF   ('&SYSLIST(1)'(1,1) EQ '(').SET0400   LIST, CONTINUE
&A       SETA  N'&SYSLIST           GET # PARAMETERS
&B       SETA  1                    INDEX INTO PARAMETERS
&C       SETA  1                    INDEX INTO FOUND SWITCHES
.SET0100 ANOP
&SW(&C)  SETC  '&SYSLIST(&B)'       ISOLATE SWITCH
&MK(&C)  SETC  'ON'                 DEFAULT MASK TO ON
         AIF   (&B EQ &A).SET0800   NO MORE, EXIT
         AIF   ('&SYSLIST(&B+1)' EQ '').SET0300   NO MASK, CONTINUE
         AIF   ('&SYSLIST(&B+1)' EQ 'ON').SET0300   ON, CONTINUE
         AIF   ('&SYSLIST(&B+1)' EQ 'OFF').SET0200   OFF, CONTINUE
         AIF   ('&SYSLIST(&B+1)' EQ 'FLIP').SET0200   FLIP, CONTINUE
        MNOTE 4,'SET001I - Mask &SYSLIST(&B+1) Invalid, ''ON'' Assumed'
         AGO   .SET0300             CONTINUE
.SET0200 ANOP
&MK(&C)  SETC  '&SYSLIST(&B+1)'     SAVE MASK
.SET0300 ANOP
&C       SETA  &C+1                 BUMP FOUND INDEX
&B       SETA  &B+2                 BUMP PARAMETER INDEX
         AIF   (&B GT &A).SET0900   NO MORE, EXIT
         AGO   .SET0100             LOOP THROUGH ALL SWITCHES
.SET0400 ANOP
&M       SETC  'ON'                 DEFAULT MASK TO ON
         AIF   ('&SYSLIST(2)' EQ '' OR '&SYSLIST(2)' EQ 'ON').SET0600
         AIF   ('&SYSLIST(2)' EQ 'OFF').SET0500   OFF, CONTINUE
         AIF   ('&SYSLIST(2)' EQ 'FLIP').SET0500   FLIP, CONTINUE
         MNOTE 4,'SET002I - Mask &SYSLIST(2) Invalid, ''ON'' Assumed'
         AGO   .SET0600             CONTINUE
.SET0500 ANOP
&M       SETC  '&SYSLIST(2)'        SAVE MASK
.SET0600 ANOP
&A       SETA  N'&Z                 PICK UP NUMBER OF SWITCHES
&B       SETA  1                    INDEX INTO SWITCHES
&C       SETA  1                    INDEX INTO FOUND SWITCHES
.SET0700 ANOP
&SW(&C)  SETC  '&Z(&B)'             SAVE SWITCH
&MK(&C)  SETC  '&M'                 SAVE MASK
&C       SETA  &C+1                 BUMP FOUND INDEX
&B       SETA  &B+1                 BUMP SWITCH INDEX
         AIF   (&B GT &A).SET0900   FINISHED, EXIT
         AGO   .SET0700             LOOP THRU ALL SWITCHES
.SET0800 ANOP
&C       SETA  &C+1                 KEEP THE NEXT INSTRUCTION HAPPY
.*
.* THEN LOCATE THE SWITCHES
.*
.SET0900 ANOP
&A       SETA  &C-1                 GET # SWITCHES REQUESTED
         AIF   (&A EQ 0).SET2900    ERROR - NO SWITCHES
&B       SETA  0                    INDEX INTO SUPPLIED SWITCHES
&C       SETA  &DCLDSW#*8           POINT TO LAST DECLARED SWITCH
&D       SETA  1                    INDEX INTO # DECLARED SWITCHES
&E       SETA  0                    COUNTER OF # BYTES FOUND
.SET1000 AIF   (&B EQ &A).SET2200   FINISHED, EXIT SEARCH
&B       SETA  &B+1                 POINT TO NEXT SWITCH
&M       SETC  '&SW(&B)'            GRAB SUPPLIED SWITCH
.SET1100 AIF   (&DCLDSW# EQ 0).SET1300   NO DECLARED, TRY GENERICS
.SET1200 AIF   ('&M' EQ '&DCLDSWB(&D)').SET1500   FOUND HIM
&D       SETA  &D+1                 INDEX TO NEXT BIT
         AIF   (&D LE &C).SET1200   KEEP LOOKING
.SET1300 AIF   (&DCLGSW# EQ 0).SET3000   ERROR - SWITCH NOT FOUND
&D       SETA  1                    INDEX INTO # GENERIC SWITCHES
.SET1400 AIF   ('&M'(1,K'&DCLGSWB(&D)) EQ '&DCLGSWB(&D)').SET1600
&D       SETA  &D+1                 NOT HIM, INDEX TO NEXT PREFIX
         AIF   (&D LE &DCLGSW#).SET1400   KEEP LOOKING
         AGO   .SET3000             ERROR - SWITCH NOT FOUND
.*
.* INCOMING SWITCH IS A DECLARED SWITCH
.*
.SET1500 ANOP
&D       SETA  (&D+7)/8             INDEX TO DECARED BYTE NAME TABLE
&N       SETC  '&DCLDSWN(&D)'       GET BYTE NAME
         AGO   .SET1700             CONTINUE
.*
.* INCOMING SWITCH IS A GENERIC SWITCH
.*
.SET1600 ANOP
&N       SETC  '&DCLGSWN(&D)'       GET BYTE NAME
.*
.* ADD SWITCH BYTE AND SWITCH BIT NAME TO A TABLE AS FOLLOWS:
.*
.*    - IF THE BYTE NAME IS NOT IN THE TABLE, ADD IT
.*    - IF IT IS BUT THE MASKS DONT MATCH, ADD IT AS A NEW ENTRY
.*    - OTHERWISE, CONCATENATE THE NEW SWITCH BIT NAME TO THE OLD
.*      SWITCH BIT NAME, WITH A '+' SIGN IN BETWEEN
.*
.SET1700 AIF   (&E EQ 0).SET2000    FIRST ENTRY, CONTINUE
&D       SETA  1                    INDEX INTO FOUND TABLES
.SET1800 AIF   ('&N' EQ '&K(&D)').SET1900   MATCHING NAME, CONTINUE
&D       SETA  &D+1                 POINT TO NEXT ENTRY
         AIF   (&D LE &E).SET1800   KEEP LOOKING
         AGO   .SET2000             THIS GUY IS NEW
.SET1900 AIF   ('&MK(&B)' EQ '&MSK(&D)').SET2100   SAME SEX TOO..
&D       SETA  &D+1                 POINT TO NEXT ENTRY
         AIF   (&D LE &E).SET1800   KEEP LOOKING
.*
.* THIS ENTRY IS A NEW BYTE NAME
.*
.SET2000 ANOP
&E       SETA  &E+1                 BUMP COUNTER OF # FOUND
&K(&E)   SETC  '&N'                 SAVE BYTE NAME
&L(&E)   SETC  '&M'                 SAVE BIT NAME
&MSK(&E) SETC  '&MK(&D)'            SAVE MASK
         AGO   .SET1000             GO, GET NEXT SUPPLIED SWITCH
.*
.* THIS ENTRY IS AN OLD ENTRY
.*
.SET2100 ANOP
&L(&D)   SETC  '&L(&D).+&M'         CONCATENATE SWITCH BIT NAMES
         AGO   .SET1000             GO, GET NEXT SUPPLIED SWITCH
.*
.* NOW TO DO SOME REAL WORK
.*
.SET2200 AIF   (&E EQ 0).SET2900    ERROR - NO SWITCHES CODED
&A       SETA  1                    INDEX INTO FOUND SWITCHES
.SET2300 ANOP
.SET2400 AIF   ('&MSK(&A)' EQ '').SET2500   OMITTED, ASSUME ON
         AIF   ('&MSK(&A)' EQ 'ON').SET2500   ON, CONTINUE
         AIF   ('&MSK(&A)' EQ 'OFF').SET2600   OFF, CONTINUE
         AIF   ('&MSK(&A)' EQ 'FLIP').SET2700   FLIP, CONTINUE
         MNOTE 4,'SET003I - Mask &MSK(&A) Invalid, ''ON'' Assumed'
.SET2500 ANOP
&O       OI    &K(&A),&L(&A)
         AGO   .SET2800             CONTINUE
.SET2600 ANOP
&O       NI    &K(&A),255-(&L(&A))
         AGO   .SET2800             CONTINUE
.SET2700 ANOP
&O       XI    &K(&A),&L(&A)
.SET2800 ANOP
&O       SETC  ''                   CLEAR LABEL
&A       SETA  &A+1                 BUMP INDEX OF SWITCHES
         AIF   (&A LE &E).SET2300   LOOP THRU ALL SWITCHES
         MEXIT                      EXIT
.SET2900 MNOTE 8,'SET004A - No Switches Specified'
         MEXIT                      EXIT
.SET3000 MNOTE 8,'SET005A - Switch &M Not Declared'
         MEND
