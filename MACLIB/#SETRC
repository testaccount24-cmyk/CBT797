         MACRO
&NAME    #SETRC &R,&FIELD=,&FORCE=N
&NAME    #NULL ,
         GBLB  &STWA                ON MEANS A WORK AREA EXISTS
         GBLC  &STPREF              WORK AREA PREFIX
         LCLC  &F                   USED TO HOLD FIELD NAME
         AIF   ('&FIELD' EQ '').SET0100   NO FIELD, CONTINUE
&F       SETC  '&FIELD'             PICK UP FIELD NAME
         AGO   .SET0200             CONTINUE
.SET0100 AIF   (NOT &STWA).SET0600   ERROR - NO FIELD PROVIDED
&F       SETC  '&STPREF.RCODE'      PICK UP WORK AREA RC FIELD
.SET0200 AIF   ('&R' EQ '').SET0400 ASSUME R15 HAS RETURN CODE
         AIF   ('&R'(1,1) EQ '(').SET0300   R IS A REGISTER
         LA    R15,&R               R15 HAS RETURN CODE
         AGO   .SET0400             CONTINUE
.SET0300 LR    R15,&R(1)            R15 HAS RETURN CODE
.SET0400 AIF   ('&FORCE' EQ 'Y' OR  '&FORCE' EQ 'YES').SET0500
         CL    R15,&F               COMPARE NEW RC TO CURRENT RC
         JNH   *+8                  KEEP THE HIGHEST
.SET0500 ST    R15,&F               RETURN CODE
         MEXIT                      EXIT
.SET0600 MNOTE 8,'SET001I - MISSING FIELD NAME'
         MEND
