         MACRO ,
&LABEL   ULUTSCAN &DEVCLASS=,      Device Class Filter                 *
               &DEVN=,             Starting Device Number              *
               &STATUS=,           Device Status Filter                *
               &VOLSER=,           Volser Filter                       *
               &LINKAGE=BRANCH,    Invocation Method                   *
               &MF=
.*
.*--------------------------------------------------------------------*
.*                                                                    *
.*       ULUTSCAN - Perform UCB scanning using the ULUT Table.        *
.*                                                                    *
.*       Optional parameters:                                         *
.*             DEVCLASS=                                              *
.*              - Device Class Filter:                                *
.*                - ALL                                               *
.*                - TAPE                                              *
.*                - COMM                                              *
.*                - DASD                                              *
.*                - DISP                                              *
.*                - UREC                                              *
.*                - CHAR                                              *
.*                - CTC                                               *
.*             DEVN=                                                  *
.*              - Starting Device Number                              *
.*                (Field in ULUPARM is updated after each call).      *
.*             STATUS=                                                *
.*              - Device Status Filter:                               *
.*                - ONLINE                                            *
.*                (If not specified ALL UCBs are returned).           *
.*             VOLSER=                                                *
.*              - Volume Serial Filter:                               *
.*                - volser or volser mask.                            *
.*                ("*" for any number of characters and               *
.*                 "?" for any single character).                     *
.*             LINKAGE=                                               *
.*              - Invocation Method (BRANCH or SYSTEM)                *
.*                (If BRANCH is used ULUTSCAN must be included).      *
.*                                                                    *
.*       Required parameters:                                         *
.*             MF=                                                    *
.*              - Parameter List ... (MF=(E,areaname))                *
.*                (Use 100 byte workarea in case of future updates).  *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         LCLA  &I,&VL,&F,&N
         LCLB  &B(8)
         LCLC  &L
.*
.**********************************************************************
.*       DEVCLASS=                                                    *
.**********************************************************************
&I       SETA  1
.DEV0010 AIF   (&I GT N'&DEVCLASS).DEV0030
         AIF   ('&DEVCLASS(&I)' EQ 'ALL').DEV0020
&B(1)    SETB  (&B(1) OR '&DEVCLASS(&I)' EQ 'TAPE')
&B(2)    SETB  (&B(2) OR '&DEVCLASS(&I)' EQ 'COMM')
&B(3)    SETB  (&B(3) OR '&DEVCLASS(&I)' EQ 'DASD')
&B(4)    SETB  (&B(4) OR '&DEVCLASS(&I)' EQ 'DISP')
&B(5)    SETB  (&B(5) OR '&DEVCLASS(&I)' EQ 'UREC')
&B(6)    SETB  (&B(6) OR '&DEVCLASS(&I)' EQ 'CHAR')
&B(7)    SETB  (&B(7) OR '&DEVCLASS(&I)' EQ 'CTC')
&I       SETA  &I+1
         AGO   .DEV0010
.DEV0020 ANOP  ,
&B(1)    SETB  1
&B(2)    SETB  1
&B(3)    SETB  1
&B(4)    SETB  1
&B(5)    SETB  1
&B(6)    SETB  1
&B(7)    SETB  1
.*
.**********************************************************************
.*       STATUS=                                                      *
.**********************************************************************
.DEV0030 ANOP  ,
&B(8)    SETB  ('&STATUS' EQ 'ONLINE')
.*
.**********************************************************************
.*       Initialize Parameter List                                    *
.**********************************************************************
         AIF   ('&MF(1)' NE 'E').DEV0090
         PUSH  USING
         IHBINNRR &MF(2)           Load Parameter List Into R1
         LR    R15,R1              A(Parameter List)
         USING ULUPARM,R15
&F       SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+4*&B(6)+2*&*
               B(7)+&B(8)
         MVI   ULUFLAG1,&F         Set Device Class / Status Flag(s)
.**********************************************************************
.*       DEVN=                                                        *
.**********************************************************************
         AIF   (K'&DEVN EQ 0).DEV0040
         IHBINNRR &DEVN            Load Parameter List Into R1
         MVC   ULUDEVN,0(R1)       Set Starting Device Number
.*
.**********************************************************************
.*       VOLSER=                                                      *
.**********************************************************************
.DEV0040 ANOP  ,
         MVI   ULUVOL,C' '         Initialize Volser
         MVC   ULUVOL+1(L'ULUVOL-1),ULUVOL
         AIF   ('&VOLSER'(1,1) EQ '(').DEV0050
&VL      SETA  K'&VOLSER
         AIF   (&VL EQ 0).DEV0060
         AIF   (&VL GT 6).DEV0100
         MVC   ULUVOL(&VL),=C'&VOLSER'
         AGO   .DEV0060
.DEV0050 ANOP  ,
         IHBINNRR &VOLSER          Load Parameter List Into R1
         MVC   ULUVOL,0(R1)        Set Volser/Mask
.*
.**********************************************************************
.*       Call ULUTSCAN                                                *
.**********************************************************************
.DEV0060 ANOP  ,
         DROP  R15
         LR    R1,R15              A(Parameter List)
         AIF   ('&LINKAGE' EQ 'SYSTEM').DEV0070
         L     R15,=V(ULUTSCAN)    A(ULUT Table Scan Routine)
         BASR  R14,R15             Get Next UCB Address
         AGO   .DEV0080
.DEV0070 ANOP  ,
         LINK  EP=ULUTSCAN         Get Next UCB Address
.DEV0080 ANOP  ,
         POP   USING
         MEXIT
.**********************************************************************
.*       Errors                                                       *
.**********************************************************************
.DEV0090 ANOP  ,
         MNOTE 8,'ULUTSCAN - Invalid MF= Value - &MF(1)'
         MEXIT ,
.DEV0100 ANOP  ,
         MNOTE 8,'ULUTSCAN - VOLSER= is too long - &VOLSER'
         MEXIT ,
         MEND  ,
