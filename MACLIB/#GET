         MACRO ,
&NAME    #GET  &T,&LV=
         GBLA  &RCPSWS(10)
         GBLC  &RCPPRE
         LCLC  &P,&LABEL
&P       SETC  '&RCPPRE'
&LABEL   SETC  '&NAME'
         AIF   (&RCPSWS(3) EQ 0).E1
         AIF   ('&LV' EQ '(0)' OR '&LV' EQ '' OR '&LV' EQ '(R0)').G2
         AIF   ('&LV'(1,1) EQ '(').GR
&LABEL   L     R0,=A(&LV)               LOAD LENGTH VALUE
&LABEL   SETC  ''
         AGO   .G2
.GR      LR    R0,&LV(1)                LOAD LENGTH VALUE
&LABEL   SETC  ''
.G2      ANOP  ,
&LABEL   L     R1,0(0,R13)              LOAD MODULE COMM AREA ADDR
         L     R15,&P.A#GET-&P.MCA(0,R1) LOAD GET ROUTINE ADDRESS
         BALR  R14,R15                  GET REQUESTED STORAGE
         MEXIT ,
.E1      MNOTE 8,'LIFO Option Not Specified On BEGIN Macro'
&LABEL   #NULL ,
         MEND  ,
