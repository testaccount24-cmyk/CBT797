         MACRO ,
         EXITWKA
.*
.*   THIS MACRO TERMINATES THE WORKAREA CSECT/DSECT STARTED BY
.*   THE BEGIN MACRO, AND CONTINUED BY THE BEGINWKA MACRO.
.*   IF THE WORK AREA IS GETMAINED BY BEGIN, IT SUPPLIES A
.*   SYMBOLIC EQUATE FOR THE WORKAREA LENGTH, SO BEGIN MACRO KNOWS
.*   HOW BIG AN AREA TO GET.
.*   THE WORKAREA CSECT/DSECT IS ENTERED AND EXITED BY CALLING THE
.*   RCPDS MACRO, WHICH SETS THE GLOBAL SETC VARIABLE &RCPDS TO
.*   THE NAME OF THE PROGRAM CSECT. WITHIN THE PROGRAM CSECT ITSELF,
.*   &RCPDS CONTAINS A NULL VALUE, AND A TEST FOR THIS IS MADE HERE.
.*   IF IT IS NULL, THIS MEANS THAT THE USER DID NOT CODE BEGINWKA,
.*   AND SO BEGINWKA IS INVOKED FOR HIM, BEFORE NORMAL PROCESSING
.*   CONTINUES.
.*   A SECONDARY FUNCTION OF THIS PROGRAM IS TO INVOKE MAPPING MACROS.
.*   EACH MAPPING MACRO HAS A TWO MEMBER GLOBAL BIT ARRAY, BOTH
.*   INITIALLY SET TO ZERO, EG FOR THE ECT, THE GLOBAL VARIABLE IS
.*   &RCPECT(2), FOR DYNALLOC &RCPS99(2), FOR IOPL &RCPIOPL(2).
.*   IF THE FIRST BIT OF THE GLOBAL SETB VARIABLE IS 1, AND THE 2ND
.*   IS ZERO, THE MAPPING MACRO IS INVOKED. THE 2ND BIT IS THEN SET
.*   TO ONE TO PREVENT A FURTHER EXPANSION OF THE MAPPING MACRO WITHIN
.*   THE SAME ASSEMBLY.
.*  A SECOND CALL TO BEGIN WILL CAUSE THE FIRST BIT TO BE SET TO
.*  ZERO, HOWEVER THE SECOND BIT WILL BE LEFT UNALTERED.
.*
.*  GLOBAL SWITCHES FOR THE ECT, UPT, PSCB, DYNALLOC DSECTS, AND LSD
         GBLB  &RCPECT(2),&RCPUPT(2),&RCPPSCB(2),&RCPS99(2)
         GBLB  &RCPLSD(2)
         GBLA  &RCPSWS(10)              ENVIRONMENT SWITCHES
.*   GLOBAL VARIABLES FOR THE CURRENT BEGIN PREFIX, WORKAREA DSECT
.*   NAME, AND PROGRAM CSECT NAME.
         GBLC  &RCPPRE,&RCPWKDS,&RCPDS,&RCPWKCS
         LCLC  &P,&CS
         AIF   ('&RCPDS' NE '').START
         BEGINWKA ,
.START   AIF   (&RCPSWS(1) GT 0).NOWK
&CS      SETC  '&RCPDS'                 PROGRAM CSECT
         AIF   ('&RCPWKDS&RCPWKCS' EQ '').RCPDS
         DS    0D                       ALIGN TO DOUBLEWORD
&P       SETC  '&RCPPRE'
&P.WKLEN EQU   *-&RCPWKDS&RCPWKCS       LENGTH OF WORK AREA
         AIF   ('&RCPWKDS&RCPWKCS' NE '&RCPWKCS').RCPDS
&P.DSEC  DSECT ,
         DS    CL(&P.WKLEN)             GEN LENGTH FOR QCON
.RCPDS   RCPDS ,
         EJECT ,
.NOWK    AIF   (NOT &RCPECT(1) OR &RCPECT(2)).TRYUPT
         IKJECT ,
L#ECT    EQU   *-ECT                    LENGTH OF ECT
&CS      CSECT ,                        REENTER MAIN CSECT
         EJECT ,
&RCPECT(2) SETB 1
.TRYUPT  AIF   (NOT &RCPUPT(1) OR &RCPUPT(2)).TRYPSCB
         #IKJUPT ,
L#UPT    EQU   *-UPT                    LENGTH OF USER PROFILE TABLE
&CS      CSECT ,                        REENTER MAIN CSECT
         EJECT ,
&RCPUPT(2) SETB 1
.TRYPSCB AIF   (NOT &RCPPSCB(1) OR &RCPPSCB(2)).TRYS99
         #IKJPSCB ,
L#PSCB   EQU   *-PSCB                   LENGTH OF PSCB
&CS      CSECT ,                        REENTER MAIN CSECT
         EJECT ,
&RCPPSCB(2) SETB 1
.TRYS99  AIF   (NOT &RCPS99(1) OR &RCPS99(2)).TRYLSD
         #DSECTS ALLOC,TYPE=DEFER
&RCPS99(2) SETB 1
.TRYLSD  AIF   (NOT &RCPLSD(1) OR &RCPLSD(2)).TRYREST
         IKJLSD ,
L#LSD    EQU   *-LSD                    LENGTH OF LSD
&CS      CSECT ,                        REENTER MAIN CSECT
         EJECT ,
&RCPLSD(2) SETB 1
.TRYREST MEND  ,
