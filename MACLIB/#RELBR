         MACRO ,
         #RELBR &OPSYN=YES
         GBLA  &SYSALVL
         GBLB  &RELBRON            Only want to do this once
.*--------------------------------------------------------------------*
.*                                                                    *
.*       This macro enables the conversion of a traditional           *
.*       register based module to a relative branch module.           *
.*                                                                    *
.*       It is recommended that a base register still be used for     *
.*       the first page of code and a base register be used for       *
.*       the data area (constants and literals).                      *
.*                                                                    *
.*       All B type instructions can still be coded as before,        *
.*       they will automatically be converted to their relative       *
.*       equivalent. If for some reason a B type instruction is       *
.*       required, simply code it with a # in front.                  *
.*                                                                    *
.*       e.g.   to code a branch to 4 beyond R14 use:                 *
.*                                                                    *
.*       #B    4(,R14)                                                *
.*                                                                    *
.*--------------------------------------------------------------------*
         AIF   (&RELBRON).MEXIT    Not the first time
&RELBRON SETB  1                   Set true
         PUSH  PRINT
         PRINT  OFF
         AIF   ('&OPSYN' EQ 'NO').NOOP
#BC      OPSYN BC
#B       OPSYN B
#BE      OPSYN BE
#BNE     OPSYN BNE
#BO      OPSYN BO
#BNO     OPSYN BNO
#BZ      OPSYN BZ
#BNZ     OPSYN BNZ
#BH      OPSYN BH
#BNH     OPSYN BNH
#BL      OPSYN BL
#BNL     OPSYN BNL
#BM      OPSYN BM
#BNM     OPSYN BNM
#BP      OPSYN BP
#BNP     OPSYN BNP
#BAL     OPSYN BAL
#BAS     OPSYN BAS
#BCT     OPSYN BCT
#BXH     OPSYN BXH
#BXLE    OPSYN BXLE
.*
BC       OPSYN BRC
B        OPSYN J
BE       OPSYN JE
BNE      OPSYN JNE
BO       OPSYN JO
BNO      OPSYN JNO
BZ       OPSYN JZ
BNZ      OPSYN JNZ
BH       OPSYN JH
BNH      OPSYN JNH
BL       OPSYN JL
BNL      OPSYN JNL
BM       OPSYN JM
BNM      OPSYN JNM
BP       OPSYN JP
BNP      OPSYN JNP
BAL      OPSYN BRAS
BAS      OPSYN BRAS
BCT      OPSYN BRCT
BXH      OPSYN BRXH
BXLE     OPSYN BRXLE
.NOOP    ANOP  ,
         SYSSTATE TEST
         AIF   (&SYSALVL NE 0).SKIPALL
         PRINT  ON
         SYSSTATE ARCHLVL=1
.SKIPALL ANOP  ,
         POP   PRINT
.MEXIT   MEXIT ,
         MEND  ,
