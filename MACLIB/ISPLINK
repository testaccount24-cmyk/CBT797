         MACRO ,
&NAME    ISPLINK &VERB,&SAVE=,&LIST=,&ERROR=,&OK=
.**********************************************************************
         LCLA  &I,&N
         LCLC  &LABEL              CURRENT INDEXED MACRO LABEL
         LCLC  &SERVICE            CURRENT SPF SERVICE
         GBLC  &SPFADDR            ISPLINK SUBROUTINE ADDRESS
         GBLC  &SPFCALL            ISPLINK PARAMETER LIST ADDRESS
         GBLA  &SPFCNT             ISPLINK VERB COUNT
         GBLC  &SPFVRB(255)        ISPLINK VERB NAME
&LABEL   SETC  'ISPL&SYSNDX'       DEFINE A LABEL INCASE NEEDED
.**********************************************************************
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP  ,
.**********************************************************************
         AIF   (N'&SYSLIST EQ 0).EXPAND
         AIF   ('&VERB' EQ '').EXPAND
         AIF   ('&VERB' EQ 'LOAD').LOAD010
         AIF   ('&VERB' EQ 'RELOAD').RELO010
         AIF   ('&VERB' EQ 'UNLOAD').UNLO010
         AGO   .SERVICE
.**********************************************************************
.LOAD010 ANOP  ,
         AIF   ('&SAVE' NE '').LOAD020
         MNOTE 8,'SAVE= Not Supplied'
         AGO   .MEND
.LOAD020 ANOP  ,
         AIF   ('&LIST' NE '').LOAD030
         MNOTE 8,'LIST= Not Supplied'
         AGO   .MEND
.LOAD030 ANOP  ,
&SPFADDR SETC  '&SAVE'
&SPFCALL SETC  '&LIST'
         AIF   ('&ERROR' NE '').LOAD040
*        LOAD  EP=ISPLINK          LOAD ISPLINK ROUTINE
         LOAD  EP=ISPLINK
         AGO   .LOAD060
.LOAD040 ANOP  ,
         AIF   ('&ERROR' EQ '*').LOAD050
*        LOAD  EP=ISPLINK,ERRET=&ERROR
         LOAD  EP=ISPLINK,ERRET=&ERROR
         AGO   .LOAD060
.LOAD050 ANOP  ,
*        LOAD  EP=ISPLINK,ERRET=&LABEL
         LOAD  EP=ISPLINK,ERRET=&LABEL
         ST    R0,&SPFADDR         SAVE ISPLINK ROUTINE ADDRESS
         B     *+8                 BRANCH AROUND ABEND
&LABEL   DS    0H
         EX    0,*                 ABEND S0C3
         AGO   .MEND
.LOAD060 ANOP  ,
         ST    R0,&SPFADDR         SAVE ISPLINK ROUTINE ADDRESS
         AGO   .MEND
.**********************************************************************
.RELO010 ANOP  ,
         AIF   ('&SAVE' NE '').RELO020
         MNOTE 8,'SAVE= Not Supplied'
         AGO   .MEND
.RELO020 ANOP  ,
         AIF   ('&LIST' NE '').RELO030
         MNOTE 8,'LIST= Not Supplied'
         AGO   .MEND
.RELO030 ANOP  ,
&SPFADDR SETC  '&SAVE'
&SPFCALL SETC  '&LIST'
         AGO   .MEND
.**********************************************************************
.UNLO010 ANOP  ,
         AIF   ('&SPFADDR' NE  '').UNLO020
         MNOTE 8,'ISPLINK Was Not Previously Loaded'
         AGO   .MEND
.UNLO020 ANOP  ,
         ICM   R0,15,&SPFADDR      CHECK IF ISPLINK HAS BEEN LOADED
         BZ    &LABEL              B. IF NOT
*        DELETE EP=ISPLINK         DELETE ISPLINK ROUTINE
         DELETE EP=ISPLINK         DELETE ISPLINK ROUTINE
         XC    &SPFADDR,&SPFADDR   CLEAR ISPLINK ROUTINE ADDRESS
&LABEL   DS    0H
         AGO   .MEND
.**********************************************************************
.SERVICE ANOP  ,
         AIF   ('&SPFADDR' EQ  '').NOTLOAD
         AIF   ('&SPFCALL' NE  '').LOOP010
.NOTLOAD ANOP  ,
         MNOTE 8,'ISPLINK Was Not Previously Loaded'
         AGO   .MEND
.*------
.*       CHECK IF SPECIFIED DIALOG SERVICE IS IN CURRENT TABLE
.*------
.LOOP010 ANOP  ,
&SERVICE SETC  '&SYSLIST(1)'
         AIF   ('&SYSLIST(1)'(1,1) EQ '''').LOOP050
         AIF   ('&SERVICE' EQ '*').LOOP050
&I       SETA  0                   INITIALIZE LOOP FACTOR
.LOOP020 ANOP  ,
&I       SETA  &I+1                ADD 1 TO COUNTER
         AIF   (&I LE 255).LOOP030
         MNOTE 8,'More Than 255 ISPLINK Functions Used'
         AGO   .MEND
.LOOP030 ANOP  ,
         AIF   (&I GT &SPFCNT).LOOP040
         AIF   ('&SPFVRB(&I)' EQ '&SERVICE').LOOP050
         AGO   .LOOP020
.LOOP040 ANOP  ,
&SPFCNT  SETA  &I
&SPFVRB(&I) SETC '&SERVICE'
.LOOP050 ANOP  ,
&I       SETA  0                   INITIALIZE LOOP FACTOR
.*------
.*       CONSTRUCT ISPF DIALOG SERVICES PARAMETER LIST
.*------
.NEXTPRM ANOP  ,
         AIF   (&I GE N'&SYSLIST).ENDLP
&I       SETA  &I+1
&N       SETA  4*(&I-1)
         AIF   (&I EQ 1).VERB
         AIF   ('&SYSLIST(&I)' EQ '*').NEXTPRM
         AIF   ('&SYSLIST(&I)' EQ '&SYSLIST(&I-1)').STPARM
         AIF   ('&SYSLIST(&I)' EQ '').BLANK
         AIF   ('&SYSLIST(&I)'(1,1) EQ '+').NUMBER
         AIF   ('&SYSLIST(&I)'(1,1) EQ '-').NUMBER
         AIF   ('&SYSLIST(&I)'(1,1) EQ '(').REG
         AIF   ('&SYSLIST(&I)'(1,1) EQ '''').LITERAL
         AIF   (T'&SYSLIST(&I) EQ 'N').NUMBER
         AIF   ('&SYSLIST(&I)'(2,1) EQ '(').ADCON
         AIF   ('&SYSLIST(&I)'(2,1) EQ '''').NUMBER
         IHBSETR &SYSLIST(&I),R0
         AGO   .STPARM
.VERB    ANOP  ,
         AIF   ('&SYSLIST(&I)'(1,1) EQ '''').LITERAL
         LA    R0,&VERB            LOAD VERB ADDRESS
         AGO   .STPARM
.LITERAL LA    R0,=C&SYSLIST(&I)   LOAD PARAMETER ADDRESS
         AGO   .STPARM
.BLANK   LA    R0,0                NULL PARAMETER ADDRESS
         AGO   .STPARM
.REG     LR    R0,&SYSLIST(&I)     LOAD PARAMETER ADDRESS
         AGO   .STPARM
.ADCON   LA    R0,=&SYSLIST(&I)    LOAD PARAMETER ADDRESS
         AGO   .STPARM
.NUMBER  LA    R0,=A(&SYSLIST(&I)) LOAD PARAMETER ADDRESS
.STPARM  ST    R0,&SPFCALL+&N
         AGO   .NEXTPRM
.ENDLP   AIF   (&I GT N'&SYSLIST).CALL
         AIF   ('&SYSLIST(&I)' EQ '').CALL
         OI    &SPFCALL+&N,128     SET END OF PARAMETER LIST
.*------
.*       CALL THE DIALOG SERVICE ROUTINE
.*------
.CALL    ANOP  ,
         LA    R1,&SPFCALL         LOAD ISPLINK PARAMETER LIST ADDRESS
         L     R15,&SPFADDR        LOAD ISPLINK ROUTINE ADDRESS
         BALR  R14,R15             CALL ISPF DIALOG MANAGER SERVICES
.*------
.*       PROCESS THE RETURN CODE
.*------
         AIF ('&ERROR' EQ '' AND '&OK' EQ '').MEND
         LTR   R15,R15             CHECK ISPLINK RETURN CODE
         AIF   ('&OK' EQ '').AFTER1
         BZ    &OK                 B. IF SUCCESSFUL
.AFTER1  ANOP  ,
         AIF   ('&ERROR' EQ '').MEND
         AIF   ('&ERROR' EQ '*').AFTER2
         BNZ   &ERROR              B. IF ERROR
         AGO   .MEND
.AFTER2  ANOP  ,
         AIF   ('&OK' NE '').AFTER3
         BZ    *+8                 B. IF SUCCESSFUL
.AFTER3  ANOP  ,
         EX    0,*                 ELSE. ABEND S0C3
         AGO   .MEND
.**********************************************************************
.EXPAND  ANOP  ,
&I       SETA  0
.EXLOOP  ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT &SPFCNT).RESET
&SERVICE SETC  '&SPFVRB(&I)'
&SERVICE DC    CL8'&SPFVRB(&I)'  . *** ISPF CONSTANT ***
         AGO   .EXLOOP
.RESET   ANOP  ,
&SPFCNT  SETA  0
         AGO   .MEND
.MEND    MEND  ,
