         MACRO
         RCPDDCB &LRECL=,&DEN=,&RECFM=,&BLKSIZE=,&DSORG=,&KEYLEN=
.**********************************************************************
.*                                                                    *
.* HISTORY OF UPDATES:                                                *
.*          #ON62764 - FIXING A BUG IN THE DSORG=PO BRANCH.      (ON) *
.*                                                                    *
.**********************************************************************
         GBLC  &DYNP
         LCLC  &C
         LCLA  &CTR,&K
         AIF   ('&LRECL' EQ '').BLK
***********************************************************************
**   DCB LRECL TEXT UNIT                                             **
***********************************************************************
         MVI   S99TUKEY+1,DALLRECL      SET LRECL KEY
         MVI   S99TUNUM+1,1             SET NUMBER FIELD
         MVI   S99TULNG+1,2             SET LENGTH FIELD
         AIF   (T'&LRECL NE 'N').LA
         MVC   S99TUPAR(2),=AL2(&LRECL.)    MOVE LRECL TO TEXT UNIT
         AGO   .LD
.LA      MVC   S99TUPAR(2),&LRECL       MOVE LRECL TO TEXT UNIT
.LD      RCPDINC 8
         SPACE
.BLK     AIF   ('&BLKSIZE' EQ '').KEY
***********************************************************************
**   DCB BLKSIZE TEXT UNIT                                           **
***********************************************************************
         MVI   S99TUKEY+1,DALBLKSZ      SET BLKSIZE KEY
         MVI   S99TUNUM+1,1             SET NUMBER FIELD
         MVI   S99TULNG+1,2             SET LENGTH FIELD
         AIF   (T'&BLKSIZE NE 'N').BA
         MVC   S99TUPAR(2),=AL2(&BLKSIZE)    MOVE BLKSIZE TO TEXT UNIT
         AGO   .BD
.BA      MVC   S99TUPAR(2),&BLKSIZE     MOVE BLKSIZE TO TEXT UNIT
.BD      RCPDINC 8
         SPACE
.KEY     AIF   ('&KEYLEN' EQ '').DEN
***********************************************************************
**   DCB KEYLEN TEXT UNIT                                            **
***********************************************************************
         MVI   S99TUKEY+1,DALKYLEN      SET KEYNEN KEY
         MVI   S99TUNUM+1,1             SET NUMBER FIELD
         MVI   S99TULNG+1,1             SET LENGTH FIELD
         AIF   (T'&KEYLEN NE 'N').KA
         MVC   S99TUPAR(1),=AL1(&KEYLEN)   MOVE KEYLEN TO TEXT UNIT
         AGO   .KD
.KA      MVC   S99TUPAR(1),&KEYLEN      MOVE KEYLEN TO TEXT UNIT
.KD      RCPDINC 8
         SPACE
.DEN     AIF   ('&DEN' EQ '').DSO
***********************************************************************
**   DCB   DEN  TEXT UNIT                                            **
***********************************************************************
         MVI   S99TUKEY+1,DALDEN        SET  DEN  KEY
         MVI   S99TUNUM+1,1             SET NUMBER KEY
         MVI   S99TULNG+1,1             SET LENGTH KEY
         AIF   (T'&DEN NE 'N').DA
         AIF   ('&DEN' EQ '0').D1
&DYNP.DEN EQU   X'03'
         AGO   .DM
.D1      AIF   ('&DEN' GT '1').D2
&DYNP.DEN EQU   X'43'
         AGO   .DM
.D2      AIF   ('&DEN' GT '2').D3
&DYNP.DEN EQU   X'83'
         AGO   .DM
.D3      AIF   ('&DEN' GT '3').D4
&DYNP.DEN EQU   X'C3'
         AGO   .DM
.D4      AIF   ('&DEN' NE '4').DE
&DYNP.DEN EQU   X'D3'
.DM      MVI   S99TUPAR,&DYNP.DEN       MOVE DENSITY TO TEXT UNIT
         AGO   .DD
.DE      MNOTE 8,'&DEN IS INVALID DENSITY,IT IS IGNORED'
         AGO   .DSO
.DA      MVC   S99TUPAR(1),&DEN         MOVE DENSITY TO TEXT UNIT
.DD      RCPDINC 8
         SPACE
.DSO     AIF   ('&DSORG' EQ '').REC
***********************************************************************
**   DCB DSORG TEXT UNIT                                             **
***********************************************************************
         MVI   S99TUKEY+1,DALDSORG      SET DSORG KEY
         MVI   S99TUNUM+1,1             SET NUMBER FIELD
         MVI   S99TULNG+1,2             SET LENGTH FIELD
         AIF   (T'&DSORG NE 'U').DSA
         AIF   ('&DSORG' NE 'PS').DS2
&C       SETC  '=X''4000'''
         AGO   .DSM
.DS2     AIF   ('&DSORG' NE 'PSU').DS3
&C       SETC  '=X''4100'''
         AGO   .DSM
.DS3     AIF   ('&DSORG' NE 'DA').DS4
&C       SETC  '=X''2000'''
         AGO   .DSM
.DS4     AIF   ('&DSORG' NE 'DAU').DS5
&C       SETC  '=X''2100'''
         AGO   .DSM
.DS5     AIF   ('&DSORG' NE 'PO').DS6
&C       SETC  '=X''0200'''                                    #ON62764
         AGO   .DSM
.DS6     AIF   ('&DSORG' NE 'POU').DSE
&C       SETC  '=X''0300'''
.DSM     MVC   S99TUPAR(2),&C           MOVE DSORG TEXT UNIT
         AGO   .DSD
.DSE     MNOTE 8,'&DSORG INVALID,IT IS IGNORED'
         AGO   .REC
.DSA     MVC   S99TUPAR(2),&DSORG       MOVE DSORG TEXT UNIT
.DSD     RCPDINC 8
         SPACE
.REC     AIF   ('&RECFM' EQ '').EXIT
**********************************************************************
**   DCB RECFM TEXT UNIT                                             **
***********************************************************************
         MVI   S99TUKEY+1,DALRECFM      SET RECFM KEY
         MVI   S99TUNUM+1,1             SET NUMBER FIELD
         MVI   S99TULNG+1,1             SET LENGTH FIELD
         AIF   (T'&RECFM NE 'U').RA
&K       SETA  1
.LOOP    ANOP
&C       SETC  '&RECFM'(&K,1)
         AIF   ('&C' NE 'M' AND '&C' NE 'R').R2
&CTR     SETA  &CTR+2
         AGO   .ELOOP
.R2      AIF   ('&C' NE 'A' AND '&C' NE 'G').R3
&CTR     SETA  &CTR+4
         AGO   .ELOOP
.R3      AIF   ('&C' NE 'S').R4
&CTR     SETA  &CTR+8
         AGO   .ELOOP
.R4      AIF   ('&C' NE 'B').R5
&CTR     SETA  &CTR+16
         AGO   .ELOOP
.R5      AIF   ('&C' NE 'D' AND '&C' NE 'T').R6
&CTR     SETA  &CTR+32
         AGO   .ELOOP
.R6      AIF   ('&C' NE 'V').R7
&CTR     SETA  &CTR+64
         AGO   .ELOOP
.R7      AIF   ('&C' NE 'F').R8
&CTR     SETA  &CTR+128
         AGO   .ELOOP
.R8      AIF   ('&C' NE 'U').RE
&CTR     SETA  &CTR+192
.ELOOP   AIF (&K EQ K'&RECFM).RX
&K       SETA  &K+1
         AGO   .LOOP
.RX      ANOP
&DYNP.RECF EQU   &CTR
         MVI   S99TUPAR,&DYNP.RECF MOVE RECFM TO TEXT UNIT
         AGO   .RD
.RE      MNOTE 8,'&RECFM INVALID,IT IS IGNORED'
         AGO   .EXIT
.RA      MVC   S99TUPAR(1),&RECFM       MOVE RECFM TO TEXT UNIT
.RD      RCPDINC 8
         SPACE
.EXIT    MEND
