         MACRO ,
&NAME    MASK  &MASKADR,                SYMBOLIC ADDRESS OF MASK       X
               &DATAADR,                SYMBOLIC ADDRESS OF DATA       X
               &MASKLEN,                MAX. LENGTH OF MASK            X
               &DATALEN                 MAX. LENGTH OF DATA
         LCLC  &X
&X       SETC  '&SYSNDX'
&NAME    STM   0,14,SAVE&X              SAVE REGISTERS
         SR    15,15                    INITIALIZE R15 TO 0
         SR    6,6                      INITIALIZE MASK PTR. TO 0
         SR    7,7                      INITIALIZE DATA PTR. TO 0
         SR    8,8                      CLEAR REG 8
         SR    9,9                      CLEAR REG 9
         MVI   BWFL&X,0
         IC    8,&MASKADR.(6)           PUT FIRST MASK CHAR IN REG 8
         IC    9,&DATAADR.(7)           PUT FIRST DATA CHAR IN REG 9
.AIF1    AIF   (T'&MASKLEN EQ 'N').LA1
         L     4,&MASKLEN               PUT MAX LENGTH OF MASK IN REG 4
         AGO   .AIF2
.LA1     LA    4,&MASKLEN               PUT MAX LENGTH OF MASK IN REG 4
.AIF2    AIF   (T'&DATALEN EQ 'N').LA2
         L     5,&DATALEN               PUT MAX LENGTH OF DATA IN REG 5
         AGO   .AIF3
.LA2     LA    5,&DATALEN               PUT MAX LENGTH OF DATA IN REG 5
.AIF3    ANOP  ,
         CLM   8,1,=C' '                DOES MASK HAVE LEADING BLANKS?
         BE    SKIP&X                   YES..SKIP LEADING MASK BLANKS

FTST&X   CLM   8,1,=C'?'                IS CURRENT MASK CHAR. A '?' ?
         BE    BKWD&X               YES..GO HANDLE BACKWARD PROCESSING
         CLM   8,1,=C'*'                IS CURRENT MASK CHAR. AN '*'?
         BE    ASTR&X                   YES..GO DO * PROCESSING
         CLR   8,9                      IS MASK CHAR EQUAL TO DATA
*                                       CHAR?
         BE    ADJP&X                   YES..GO ADJUST PTRS TO NEXT
*                                       MASK CHAR AND DATA CHAR
         LA    15,4                     NO..SET RETURN CODE
         B     EXIT&X                   GO EXIT

***********************************************************************
*              SKIP ANY LEADING BLANKS IN MASK                        *
***********************************************************************

SKIP&X   IC    8,&MASKADR.(6)           PUT MASK CHAR IN REG 8
         CLM   8,1,=C' '                IS MASK CHAR BLANK?
         BNE   FTST&X                   NO..GO CONTINUE PROCESSING
         LA    6,1(,6)                  YES..POINT TO NEXT CHAR
         CR    6,4                      IS THIS THE END OF THE MASK?
         BNE   SKIP&X                   NO..CONTINUE
         LA    15,16                    YES..MASK IS ALL BLANK..SET RET
         B     EXIT&X                   CODE AND GO EXIT

***********************************************************************
*   ? FOUND IN MASK..ELIMINATE TRAILING BLANKS IN MASK AND DATA,      *
*   AND BEGIN PROCESSING FROM END OF BOTH FIELDS. WHEN ? ENCOUNTERED  *
*   AGAIN..PROCESSING COMPLETE.                                       *
***********************************************************************

BKWD&X   TM    BWFL&X,X'80'             ARE WE GOING BACKWARDS ALREADY?
         BZ    STBK&X                NO..INITIATE BACKWARD PROCESSING
         CR    3,6                      YES..IS IT THE SAME '?'  ?
         BE    EXIT&X                   YES..OK..EXIT
         LA    15,16                NO..MORE THAN ONE ? IN MASK..ERROR
         B     EXIT&X                   GO EXIT
STBK&X   OI    BWFL&X,X'80'             SET BACKWARD INDICATOR
         LR    3,6                      STORE PTR TO ? IN MASK
***********************************************************************
*            ELIMINATE TRAILING BLANKS IN MASK AND DATA               *
***********************************************************************
         LR    6,4                      LOAD MAXIMUM LENGTH OF MASK
         LR    7,5                      LOAD MAXIMUM LENGTH OF DATA
MASK&X   BCTR  6,0                      DECREASE MAXIMUM LENGTH OF MASK
         IC    8,&MASKADR.(6)           PUT MASK CHAR IN REG 8
         CLM   8,1,=C' '                IS MASK CHAR. BLANK?
         BE    MASK&X                   YES..GO CHECK NEXT ONE
MEMB&X   BCTR  7,0                      DECREASE MAXIMUM LENGTH OF DATA
         IC    9,&DATAADR.(7)           PUT DATA CHAR IN REG 9
         CLM   9,1,=C' '                IS DATA CHAR. BLANK?
         BNE   FTST&X                   NO..GO CONTINUE PROCESSING..ALL
*                                       TRAILING BLANKS ELIMINATED
         LTR   7,7                      ARE WE AT BEGINNING OF DATA?
         BNZ   MEMB&X                   NO..GO CHECK NEXT CHAR
         LA    15,4                 YES..DATA IS ALL BLANKS..SET RET
         B     EXIT&X                   CODE AND EXIT

***********************************************************************
*                      * FOUND IN MASK                                *
***********************************************************************

ASTR&X   CLM   9,1,=C' '                IS DATA CHAR BLANK?
         BNE   ADJP&X                NO..MATCH OK..GO ADJUST PTRS
         LA    15,4                     YES..NO MATCH..SET RETURN CODE
         B     EXIT&X                   AND GO EXIT

***********************************************************************
*   INCREASE OR DECREASE PTRS DEPENDING ON WHETHER PROCESSING         *
*   BACKWARDS OR FORWARDS                                             *
***********************************************************************

ADJP&X   TM    BWFL&X,X'80'             PROCESSING BACKWARDS?
         BZ    INCR&X                   NO..GO INCREASE PTRS
         BCTR  6,0                      YES..DECREASE PTRS
         BCTR  7,0
         IC    8,&MASKADR.(6)           PUT MASK CHAR IN REG 8
         IC    9,&DATAADR.(7)           PUT DATA CHAR IN REG 9
         B     FTST&X                   CONTINUE PROCESSING

INCR&X   LA    6,1(,6)                  INCREASE MASK PTR
         CR    6,4                      END OF MASK?
         BE    EXIT&X                   YES..FINISHED..GO EXIT
         IC    8,&MASKADR.(6)           PUT MASK CHAR IN REG 8
         CLM   8,1,=C' '                IS MASK CHAR BLANK?
         BE    EXIT&X                   YES..FINISHED..GO EXIT
         LA    7,1(,7)                  NO..INCREASE DATA PTR
         IC    9,&DATAADR.(7)           PUT DATA CHAR IN REG 9
         B     FTST&X                   GO CONTINUE PROCESSING

BWFL&X   DC    X'00'
SAVE&X   DS    15F

EXIT&X   LM    0,14,SAVE&X              RESTORE REGISTERS
         MEND  ,
