         MACRO
&NAME    GOIFM &ON=,&OFF=,&MIXED=,&ELSE=,&NOTALL=,&ANY=
         GBLA  &RCPDSW0                NO OF RETURNED BYTE NAMES
         GBLB  &RCPDSW3(20)            INVERT INDICATORS
         GBLC  &RCPDSW1(20),&RCPDSW2(20) BYTE AND BIT NAMES
         GBLB  &GOIFREL                USE RELATIVE
         GBLB  &STF(24)                #START/#STACK FLAGS
         LCLB  &RELOP
         LCLC  &OP
         LCLA  &I,&J,&K,&L,&N
         LCLC  &A,&B,&C,&M(2),&LABEL
&RELOP   SETB  (&STF(19) OR &GOIFREL)  SET REL ADDR INDICATOR
&LABEL   SETC  '&NAME'
&N       SETA  N'&ON+N'&OFF+N'&ANY+N'&NOTALL+N'&ELSE
         AIF   (K'&MIXED EQ 0).TON
      MNOTE 4,'MIXED OPTION NOT SUPPORTED WITH MULTIPLE SWITCH BYTES'
         AIF   (&N EQ 0).EXIT
.TON     AIF   (K'&ON GT 0).ON
         AIF   (K'&OFF GT 0).OFF
         AIF   (K'&ANY GT 0).ANY
         AIF   (K'&NOTALL GT 0).NOTALL
         MEXIT OOPS
.ON      AIF   (&N GT 1).ONPLUS
&L       SETA  4                       LENGTH OF FINAL BRANCH
&M(1)    SETC  'O'                     BRANCH MASK
&M(2)    SETC  'Z'                     BRANCH MASK WHEN INVERTED
&B       SETC  '&ON'                   BRANCH LABEL
&OP      SETC  'J'
         AIF   (&RELOP).RELA
&OP      SETC  'B'                     ASSUME NOT REL
.RELA    ANOP  ,
         AIF   ('&ON'(1,1) NE '(').ONOFF WAS IT A REG?
&L       SETA  2                       IF SO, FINAL BR LENGTH = 2
&C       SETC  'R'                     REGISTER OP CODE
&OP      SETC  'B'
&B       SETC  '&ON'(2,K'&ON-2)        STRIP BRACKETS
         AGO   .ONOFF
.OFF     AIF   (&N GT 1).OFFPLUS
&L       SETA  4                       LENGTH OF FINAL BRANCH
&M(1)    SETC  'Z'                     BRANCH MASK
&M(2)    SETC  'O'                     BRANCH MASK WHEN INVERTED
&B       SETC  '&OFF'                  BRANCH LABEL
&OP      SETC  'J'
         AIF   (&RELOP).RELB
&OP      SETC  'B'                     ASSUME NOT REL
.RELB    ANOP  ,
         AIF   ('&OFF'(1,1) NE '(').ONOFF WAS IT A REG?
&L       SETA  2                       IF SO, FINAL BR LENGTH = 2
&C       SETC  'R'                     REGISTER OP CODE
&OP      SETC  'B'
&B       SETC  '&OFF'(2,K'&OFF-2)      STRIP BRACKETS
.ONOFF   ANOP  ,
&L       SETA  (&RCPDSW0-1)*8+&L        INITIAL BR AROUND LEN
.ONOFF1  AIF   (&I GE &RCPDSW0).EXIT
&I       SETA  &I+1
&LABEL   TM    &RCPDSW1(&I),&RCPDSW2(&I)
&LABEL   SETC  ''                      ONLY WANT LABEL 1ST TIME
&A       SETC  '&M(&RCPDSW3(&I)+1)'  BRANCH MASK
         AIF   (&I EQ &RCPDSW0).ONOFFL GO IF FINAL BRANCH
         &OP.N&A *+&L
&L       SETA  &L-8                    DECREMENT BR LENGTH
         AGO   .ONOFF1
.ONOFFL  &OP&A&C &B
         AGO   .EXIT
.ANY     ANOP  ,
&M(1)    SETC  'Z'                     BRANCH MASK
&M(2)    SETC  'O'                     BRANCH MASK IF INVERTED
&B       SETC  '&ANY'                  BRANCH TO LABEL
&OP      SETC  'J'
         AIF   (&RELOP).RELC
&OP      SETC  'B'                     ASSUME NOT REL
.RELC    ANOP  ,
         AIF   ('&ANY'(1,1) NE '(').ANYNALL  CHECK FOR REG
&C       SETC  'R'                     REG OP CODE
&OP      SETC  'B'
&B       SETC  '&ANY'(2,K'&ANY-2)      STRIP OFF BRACKETS
         AGO   .ANYNALL
.NOTALL  ANOP  ,
&M(1)    SETC  'O'                     BRANCH MASK
&M(2)    SETC  'Z'                     BRANCH MASK IF INVERTED
&B       SETC  '&NOTALL'               BRANCH TO LABEL
&OP      SETC  'J'
         AIF   (&RELOP).RELD
&OP      SETC  'B'                     ASSUME NOT REL
.RELD    ANOP  ,
         AIF   ('&NOTALL'(1,1) NE '(').ANYNALL  CHECK FOR REG
&C       SETC  'R'                     REG OP CODE
&OP      SETC  'B'
&B       SETC  '&NOTALL'(2,K'&NOTALL-2) STRIP OFF BRACKETS
.ANYNALL AIF   (&I GE &RCPDSW0).ELSE
&I       SETA  &I+1
&LABEL   TM    &RCPDSW1(&I),&RCPDSW2(&I)
&LABEL   SETC  ''                      ONLY WANT LABEL 1ST TIME
&A       SETC  '&M(&RCPDSW3(&I)+1)&C'   GET BRANCH MASK
         &OP.N&A &B
         AGO   .ANYNALL
.ELSE    AIF   ('&ELSE' EQ '').END
         AIF   ('&ELSE'(1,1) EQ '(').ELSER
         &OP   &ELSE
         MEXIT ,
.ELSER   BR    &ELSE(1)
         MEXIT ,
.ONPLUS  ANOP  ,
.OFFPLUS MNOTE 'MULTIPLE KEYWORDS NOT SUPPORTED YET'
.EXIT    AIF   ('&LABEL' EQ '').END
&LABEL   DS    0H
.END     MEND  ,
