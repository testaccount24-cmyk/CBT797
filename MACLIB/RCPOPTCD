         MACRO ,
&NAME    RCPOPTCD &CODES
         LCLA  &CNT,&VAL
         LCLC  &O,&REG
         AIF   ('&CODES' EQ '').EXIT
         AIF   ('&CODES'(1,2) EQ '((').REGS
&CNT     SETA  0
&VAL     SETA  0
.*
.PROCESS ANOP  ,
&CNT     SETA  &CNT+1
&O       SETC  '&CODES(&CNT)'
         AIF   ('&O' EQ '').DONE
.*
.*       OPTCD=R OR OPTCD=J
         AIF   ('&O' EQ 'R' OR '&O' EQ 'J').A1
         AGO   .A2
.A1      ANOP  ,
&VAL     SETA  &VAL+1
         AGO   .PROCESS
.*
.*       OPTCD=T
.A2      AIF   ('&O' NE 'T').A3
&VAL     SETA  &VAL+2
         AGO   .PROCESS
.*
.*       OPTCD=Z
.A3      AIF   ('&O' NE 'Z').A4
&VAL     SETA  &VAL+4
         AGO   .PROCESS
.*
.*       OPTCD=A OR OPTCD=Q
.A4      AIF   ('&O' EQ 'A' OR '&O' EQ 'Q').A5
         AGO   .A6
.A5      ANOP  ,
&VAL     SETA  &VAL+8
         AGO   .PROCESS
.*
.*       OPTCD=F OR OPTCD=H OR OPTCD=O
.A6      AIF   ('&O' EQ 'F' OR '&O' EQ 'H' OR '&O' EQ 'O').A5
         AGO   .A8
.A7      ANOP  ,
&VAL     SETA  &VAL+64
         AGO   .PROCESS
.*
.*       OPTCD=B
.A8      AIF   ('&O' NE 'B').A9
&VAL     SETA  &VAL+128
         AGO   .PROCESS
.*
.A9      MNOTE 8,'INVALID OPTCD= VALUE (&O)'
         AGO   .PROCESS
.*
.DONE    AIF   ('&VAL' EQ '0').EXIT
         AGO   .OPDATA
.*
.REGS    ANOP  ,
&REG     SETC  '&CODES'(3,K'&CODES-4)  REMOVE DOUBLE PARENS
***********************************************************************
**       OPTCD= Text Unit                                            **
***********************************************************************
         MVC   S99TUKEY,=AL2(DALOPTCD) Set Key
         MVI   S99TUNUM+1,1            Set Count
         MVI   S99TULNG+1,1            Set Length
         STC   &REG,S99TUPAR           Set TU Parameter
         RCPDINC 8
         AGO   .EXIT
.*
.OPDATA  ANOP  ,
***********************************************************************
**       OPTCD= Text Unit                                            **
***********************************************************************
         MVC   S99TUKEY,=AL2(DALOPTCD) Set Key
         MVI   S99TUNUM+1,1            Set Count
         MVI   S99TULNG+1,1            Set Length
         MVI   S99TUPAR,&VAL           Set TU Parameter
         RCPDINC 8
.EXIT    MEND  ,
