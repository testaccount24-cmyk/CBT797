         MACRO
&NAME    #GETFL &FLD,                   ADDR OF CTGFL BEING ACCESSED   &
               &A,                      STRG OR REG TO GET ADDR        &
               &L,                      STRG OR REG TO GET LENGTH      &
               &ZERO=                   BRANCH ADDRESS
.*********************************************************************
.*                                                                   *
.*   THIS MACRO IS USED TO RETRIEVE A PARTICULAR FIELD IN A CATALOG  *
.* PARAMETER LIST.  THE FORMAT IS:                                   *
.*      #GETFL CTGFLADDR,PTR(,LENREG)                                *
.*            CTGFLADDR -- IS THE ADDRESS OF THE CTGFL FROM WHICH    *
.*                         DATA IS TO BE RETRIEVED.                  *
.*            PTR -------- IS THE ADDRESS OR REGISTER WHERE THE      *
.*                         ADDRESS OF THE DATA WILL BE PLACED.       *
.*                         VALID REGS (1-14)                         *
.*            LENREG ----- IS THE ADDRESS OR REGISTER WHERE THE      *
.*                         LENGTH OF THE DATA IS TO BE PLACED.       *
.*            ZERO= ------ IS THE ADDRESS TO BE BRANCHED TO IF THE   *
.*                         POINTER FIELD IS ZERO                     *
.*                                                                   *
.*  NOTE. THE MACROS IEZCTGPL AND IEZCTGFL MUST BE CODED TO USE      *
.*        THIS MACRO.                                                *
.*                                                                   *
.*********************************************************************
         LCLC  &REG                         *
         AIF   ('&FLD' EQ '').E1            *
         AIF   ('&A' EQ '').E2              *
         PUSH  USING                        |
         USING CTGFL,15                     | ADDRESS THE CTGFL
         AIF   ('&FLD'(1,1) EQ '(').FR      *
&NAME    LA    15,&FLD                      | REG 1 -> CTGFL
         AGO   .L1                          *
.FR      ANOP                               *
&REG     SETC  '&FLD'(2,K'&FLD-2)           * STRIP PARENTHESIS
&NAME    LR    15,&REG                      | REG 1 -> CTGFL
.L1      AIF   ('&A'(1,1) EQ '(').AR        * PTR SPECD BY REG
         AIF   ('&ZERO' NE '').L11          * CHECK FOR ZERO=
         L     0,CTGFLPT                    | PUT THE ADDRESS OF THE
         ST    0,&A                         | DATA INTO STORAGE
         AGO   .L2                          *
.L11     ANOP                               *
         ICM   0,B'1111',CTGFLPT            | GET POINTER TO DATA
         ST    0,&A                         | DATA INTO STORAGE
         BZ    &ZERO                        | CHECK FOR ZERO POINTER
         AGO   .L2                          *
.AR      ANOP                               *
&REG     SETC  '&A'(2,K'&A-2)               * STRIP PARENTHESIS
         AIF   ('&ZERO' NE '').AR1          * CHECK FOR ZERO=
         L     &REG,CTGFLPT                 | GET POINTER TO DATA
         AGO   .L2                          *
.AR1     ANOP                               *
         ICM   &REG,B'1111',CTGFLPT         | GET POINTER TO DATA
         BZ    &ZERO                        | CHECK FOR ZERO POINTER
.L2      AIF   ('&L' EQ '').L3              * SKIP IF NO LENGTH REQUEST
         AIF   ('&L'(1,1) EQ '(').LR        * LENGTH TO GO IN REGISTER
         L     0,CTGFLNG                    | SAVE THE LENGTH OF THE
         ST    0,&L                         | DATA INTO STORAGE
         AGO   .L3                          *
.LR      ANOP                               *
&REG     SETC  '&L'(2,K'&L-2)               * STRIP PARENTHESIS
         L     &REG,CTGFLNG                 | GET LENGTH OF DATA
.L3      ANOP                               *
         DROP  15                           | REMOVE ADDRESSABILITY
         POP   USING                        |
         MEXIT ,                            *
.E1      MNOTE 8,'FIELD Address Missing'    *
         MEXIT ,                            *
.E2      MNOTE 8,'FIELD Result Pointer Missing'
         MEXIT ,                            *
         MEND  ,                            *
