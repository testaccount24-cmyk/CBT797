         MACRO
&NAME    DEFAULT &DSN=,&BUFFER=,&ERROR=,&NOTCAT=,&QUAL=
         GBLB  &RCPDFPL(2)
         GBLC  &EXECNAM,&RCPPRE,&RCPDFEP,&RCPDFER
         LCLA  &I,&J
         LCLC  &R,&DFPB,&ENDLAB,&LABEL,&P1,&P2,&C
         AIF   (NOT &RCPDFPL(1)).ERROR1
&LABEL   SETC  '&NAME'
&DFPB    SETC  '&RCPPRE.DFPB+DFPB'
&ENDLAB  SETC  '&RCPPRE&SYSNDX'
         AIF  (N'&SYSLIST EQ 0 AND '&DSN' EQ '' AND '&BUFFER' EQ '').GO
         AIF   ('&BUFFER' NE '').BF1
         AIF   ('&DSN' EQ '').ENDDSN
&LABEL   LA    R1,&RCPPRE.DSNB          LOAD DSNAME BUFFER ADDRESS
         AGO   .STR1
.BF1     AIF   ('&BUFFER'(1,1) EQ '(').RB
&LABEL   LA    R1,&BUFFER               LOAD ADDRESS OF DSNAME BUFFER
         AGO   .STR1
.RB      ANOP  ,
&LABEL   LA    R1,&BUFFER(1)            LOAD ADDRESS OF DSNAME BUFFER
.STR1    STCM  R1,7,&DFPB.DSN-DFPB+1    STORE IT IN DFPB
&LABEL   SETC  ''
.DSN     AIF   ('&DSN' EQ '').ENDDSN
         AIF   ('&DSN'(1,1) EQ '(').DSNR
         TM    &DSN+6,64                WAS DSNAME ENTERED IN QUOTES?
         BO    &ENDLAB                  IF SO, BYPASS DEFAULT ROUTINE
         L     R14,&DSN                 LOAD DSNAME ADDRESS
         LH    R15,&DSN+4                AND LENGTH
         AGO   .STR15
.DSNR    TM    6(&DSN(1)),64            WAS DSNAME ENTERED IN QUOTES?
         BO    &ENDLAB                  IF SO, BYPASS DEFAULT ROUTINE
         L     R14,0(,&DSN(1))          LOAD DSNAME ADDRESS
         LH    R15,4(,&DSN(1))          AND LENGTH
.STR15   STH   R15,0(,R1)               STORE LENGTH INTO DSN BUFFER
         LA    R0,2(,R1)                POINT PAST 2 BYTE HEADER
         LA    R1,44                    LOAD MOVE LENGTH
         ICM   R15,8,=C' '              INSERT PADDING CHARACTER
         MVCL  R0,R14                   MOVE DSNAME TO BUFFER, AND PAD
.ENDDSN  ANOP  ,
.LOOP1   ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT N'&SYSLIST).EXITL1
&C       SETC  '&SYSLIST(&I)'
         AIF   ('&C' NE 'USERID' AND '&C' NE 'USID').T2
&P2      SETC  '&P2+DFPBUID'
         AGO   .LOOP1
.T2      AIF   ('&C' NE 'MSG' AND '&C' NE 'MESSAGE').T3
&P2      SETC  '&P2+DFPBMSG'
         AGO   .LOOP1
.T3      AIF   ('&C' NE 'RET' AND '&C' NE 'RETQUAL').T4
&P2      SETC  '&P2+DFPBRET'
         AGO   .LOOP1
.T4      ANOP  ,
         MNOTE 4,'Invalid Option ''&C'', Ignored'
         AGO   .LOOP1
.EXITL1  AIF   ('&QUAL' NE '').NQUAL
.*
.NQUAL   ANOP  ,
         AIF   ('&P2' EQ '').GO
&P2      SETC  '&P2'(2,K'&P2-1)
&LABEL   MVI   &DFPB.CNTL-DFPB,&P2
&LABEL   SETC  ''
.GO      ANOP  ,
&LABEL   LA    R1,&RCPPRE.DFPL          LOAD PARM LIST ADDRESS
         AIF   ('&RCPDFEP' EQ '').LINK
         L     R15,&RCPDFEP             LOAD DEFAULT ROUTINE EP ADDR
         BALR  R14,R15                  AND INVOKE IT
         AGO   .ENDLINK
.LINK    ANOP  ,
*        INVOKE DEFAULT SERVICE ROUTINE
         CALLTSSR EP=IKJEHDEF
.ENDLINK ANOP  ,
         AIF   ('&ERROR' EQ 'NO').NERR
         AIF   ('&ERROR' EQ '').TDFER
&RCPDFER SETC  '&ERROR'
         AGO   .DFER
.TDFER   AIF   ('&RCPDFER' EQ '').NERR
.DFER    CL    R15,=A(4)                DID DEFAULT SUCCEED?
         BNH   *+12                     YES, OK
         CL    R15,=A(20)               WAS DATASET NOT IN CATALOG?
         BNE   &ERROR                   IF NOT, GO TO ERROR ROUTINE
         AIF   ('&DSN' EQ '').ENDLAB
.NERR    L     R15,&DFPB.DSN-DFPB       LOAD DSN BUFFER ADDR
         AIF   ('&DSN'(1,1) EQ '(').DSNR2
         MVC   &DSN+4(2),0(R15)         UPDATE PDE LENGTH FIELD
         LA    R15,2(,R15)               POINT PAST HEADER
         ST    R15,&DSN                  AND UPDATE PDE POINTER
         AGO   .ENDLAB
.DSNR2   MVC   4(2,&DSN(1)),0(R15)      UPDATE PDE LENGTH FIELD
         LA    R15,2(,R15)               POINT PAST HEADER
         ST    R15,0(,&DSN(1))           AND UPDATE PDE POINTER
.ENDLAB  ANOP  ,
&ENDLAB  DS    0H
.EXIT    MEXIT ,
.ERROR1  MNOTE 8,'Default Option Not Specified On BEGIN Macro'
         MEND
