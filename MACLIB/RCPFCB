         MACRO ,
         RCPFCB &NAME,&OPT
         LCLC  &O,&REG
         GBLC  &DYNP
         AIF   ('&NAME' EQ '').M400
***********************************************************************
**       FCB Name Text Unit                                          **
***********************************************************************
         AIF   ('&NAME'(1,1) EQ '''').M300
         AIF   ('&NAME'(1,1) EQ '(').M100
         L     R14,&NAME           A(NAME)
         LH    R2,&NAME+4          L(NAME)
         AGO   .M200
.M100    L     R14,0&DSN           A(NAME)
         LH    R2,4&DSN            L(NAME)
.M200    MVC   S99TUKEY,=AL2(DALFCBIM) Set Key
         MVI   S99TUNUM+1,1            Set Count
         STH   R2,S99TULNG             Set Length
         SHI   R2,1
         EX    R2,&DYNP.MVC            Set TU Parameter
         RCPDINC 14
         AGO   .M400
.M300    RCPBTU DALFCBIM,1,&NAME
.M400    AIF   ('&OPT' EQ '').EXIT
***********************************************************************
**       FCB Option Text Unit                                        **
***********************************************************************
         MVC   S99TUKEY,=AL2(DALFCBAV) Set Key
         MVI   S99TUNUM+1,1            Set Count
         MVI   S99TULNG+1,1            Set Length
         AIF   ('&OPT'(1,1) EQ '(').M800
         AIF   ('&OPT' NE 'ALIGN').M500
&O       SETC  'X''08'''
         AGO   .M700
.M500    AIF   ('&OPT' NE 'VERIFY').M600
&O       SETC  'X''04'''
         AGO   .M700
.M600    MNOTE 8,'FCB OPTION IS NOT "ALIGN" OR "VERIFY"'
         MEXIT ,
.M700    MVI   S99TUPAR,&O             Set TU Parameter
         AGO   .M900
.M800    ANOP  ,
&REG     SETC  '&OPT'(2,K'&OPT-2)      Remove Brackets
         STC   &REG,S99TUPAR           Set TU Parameter
.M900    RCPDINC 8
.EXIT    MEND  ,
