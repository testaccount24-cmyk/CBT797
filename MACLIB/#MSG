         MACRO ,
&NAME    #MSG  &OFFSET=0,&TYPE=ABSOLUTE,&ALIGN=
         GBLA  &RCPTXTL
         LCLA  &L,&I
         LCLC  &A,&C
&L       SETA  4                       START WITH HEADER LENGTH
&I       SETA  1
         AIF   (N'&SYSLIST EQ 0).EXIT
&A       SETC  'AL2'
         AIF   ('&ALIGN' NE 'YES').NOALIGN
&A       SETC  'Y'
.NOALIGN ANOP  ,
         AIF   (N'&SYSLIST GT 1).TTYPE
         AIF   ('&SYSLIST(1)'(1,1) NE '''').ERROR
         RCPTXTL &SYSLIST(1)
&L       SETA  &RCPTXTL+4
&NAME    DC    &A.(&L,&OFFSET)
         DC    C&SYSLIST(1)

         MEXIT
.TTYPE   AIF   ('&TYPE' EQ 'SYMBOLIC'(1,K'&TYPE)).SYM
         AIF   ('&TYPE' EQ 'ABSOLUTE'(1,K'&TYPE)).LOOP1
         MNOTE 4,'TYPE=&TYPE Invalid, TYPE=ABSOLUTE Used'
.LOOP1   ANOP  ,
         AIF   ('&SYSLIST(&I)'(1,1) NE '''').ERROR
         RCPTXTL &SYSLIST(&I)
&L       SETA  &L+&RCPTXTL             INCREMENT LENGTH
&I       SETA  &I+2                    INCREMENT INDEX
         AIF   (&I LE N'&SYSLIST).LOOP1
&NAME    DC    Y(&L,&OFFSET)           SEGMENT LENGTH AND OFFSET
&I       SETA  0
&L       SETA  0
.LOOP2   ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT N'&SYSLIST).EXIT
&L       SETA  &L+K'&SYSLIST(&I)-2
         DC    C&SYSLIST(&I)
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I)' EQ '').LOOP2
&C       SETC  '&SYSLIST(&I)'
&C       EQU   &L
         AGO   .LOOP2

.EXIT    ANOP  ,
         MEXIT
.SYM     AIF   ('&NAME' EQ '').NONAME
         AIF   (K'&NAME GT 7).LENGT7
&NAME    DC    &A.(&L,&OFFSET)
&I       SETA  0
.LOOP3   ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT N'&SYSLIST).SETL
         AIF   ('&SYSLIST(&I)'(1,1) NE '''').ERROR2
         DC    C&SYSLIST(&I)
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I)' EQ '').LOOP3
&C       SETC  '&SYSLIST(&I)'
&C       EQU   *-&NAME-4
         AGO   .LOOP3
.SETL    ANOP  ,
&NAME.L  EQU   *-&NAME                 TOTAL SEGMENT LENGTH

         MEXIT
.NONAME  MNOTE 8,'NAME Operand Required With TYPE=SYMBOLIC'
         MEXIT
.LENGT7  MNOTE 8,'NAME Must Be 1-7 Chars With TYPE=SYMBOLIC'
         MEXIT
.ERROR   MNOTE 8,'Message Segments Should Be Entered In Quotes'
         MEXIT
.ERROR2  MNOTE 8,'Message Segments Should Be Entered In Quotes'
&NAME.L  EQU   *-&NAME                 TOTAL SEGMENT LENGTH
         MEND
