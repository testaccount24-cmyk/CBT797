         MACRO ,
&NAME    TERMIO &X,&Y,&Z,&OUTPUT=,&INPUT=,&ENTRY=,&ERROR=,             X
               &TERMPUT=,&TERMGET=,&DELETE=,&DATASET=,&STORAGE=
         GBLB &RCPPTPB(2),&RCPGTPB(2),&RCPPGPB(2),&RCPSTPB(2)
         GBLC &RCPPRE,&RCPPTEP,&RCPGTEP,&RCPPGEP,&RCPSTEP
         GBLC &RCPSTER,&RCPPTER,&RCPGTER,&RCPPGER
         LCLA  &L,&I,&J
         LCLB  &FLUSH
         LCLC  &C,&R
         AIF   ('&NAME' EQ '').TX
&NAME    DS    0H
.TX      AIF   ('&X' EQ '').E1
&L       SETA  K'&X
         AIF   (&L GT 7).E2
         AIF   ('&X' EQ 'FREEBUF'(1,&L)).FREEBUF
         AIF   ('&X' EQ 'PUTLINE'(1,&L)).PUTLINE
         AIF   ('&X' EQ 'GETLINE'(1,&L)).GETLINE
         AIF   ('&X' EQ 'PUTGET '(1,&L)).PUTGET
         AIF   ('&X' EQ 'STACK  '(1,&L)).STACK
&FLUSH   SETB  1
         AIF   ('&X' EQ 'FLUSH  '(1,&L)).STACK
.E2      MNOTE 8,'&X Is An Invalid Function'
         MEXIT ,
.E1      MNOTE 8,'No Operation Specified'
         MEXIT ,
.FREEBUF ANOP  ,
&R       SETC  'R1'
         AIF   ('&Y' EQ '' OR '&Y' EQ '(1)' OR '&Y' EQ '(R1)').FLR0
         AIF   ('&Y' EQ 'PUTGET').FPG
         AIF   ('&Y' EQ 'GETLINE').FGT
         AIF   ('&Y'(1,1) NE '(').FLR1
&R       SETC  '&Y(1)'
         AGO   .FLR0
.FPG     AIF   (&RCPPGPB(1)).FPG1
      MNOTE 4,'PUTGET Not Specified On BEGIN Macro, Errors May Occur'
.FPG1    L     R1,&RCPPRE.PGPB+PGPBIBUF-PGPB  LOAD INPUT LINE ADDRESS
         AGO   .FLR0
.FGT     AIF   (&RCPGTPB(1)).FGT1
         MNOTE 4,'GETL Not Specified On BEGIN Macro, Errors May Occur'
.FGT1    L     R1,&RCPPRE.GTPB+GTPBIBUF-GTPB  LOAD INPUT LINE ADDRESS
         AGO   .FLR0
.FLR1    L     R1,&Y                   LOAD INPUT LINE ADDRESS
.FLR0    LH    R0,0(0,&R)              LOAD LINE LENGTH
         FREEMAIN RU,LV=(0),A=(&R),SP=1
         MEXIT ,
.PUTLINE AIF   (&RCPPTPB(1)).PUTL1
         MNOTE 4,'PUTL Not Specified On BEGIN Macro, Errors May Occur'
.PUTL1   AIF   ('&ENTRY' EQ '').TPTEP
         AIF   ('&ENTRY' EQ 'NO').PUTL2
&R       SETC  '(15)'
         AIF   ('&ENTRY' EQ '(15)').PUTL2
&RCPPTEP SETC  '&ENTRY'
         L     R15,&ENTRY              LOAD PUTLINE ENTRY POINT
         AGO   .PUTL2
.TPTEP   AIF   ('&RCPPTEP' EQ '').PUTL2
&R       SETC  '(15)'
         L     R15,&RCPPTEP            LOAD PUTLINE EP ADDRESS
.PUTL2   PUTLINE PARM=&RCPPRE.PTPB,MF=(E,&RCPPRE.IOPL),ENTRY=&R,       X
               OUTPUT=&OUTPUT,TERMPUT=&TERMPUT
         AIF   ('&ERROR' EQ '').TPTER
         AIF   ('&ERROR' EQ 'NO').EXIT
&RCPPTER SETC  '&ERROR'
.PTER    LTR   R15,R15                 TEST RETURN CODE
         BNZ   &RCPPTER                 AND BRANCH IF NON ZERO
         MEXIT ,
.TPTER   AIF   ('&RCPPTER' NE '').PTER
.EXIT    MEXIT ,
.STACK   AIF   (&RCPSTPB(1)).STACK1
         MNOTE 4,'FLUSH Or STACK Not Specified On BEGIN Macro, Errors MX
               Ay Occur'
.STACK1  AIF   ('&ENTRY' EQ '').TSTEP
         AIF   ('&ENTRY' EQ '').STACK2
&R       SETC  '(15)'
         AIF   ('&ENTRY' EQ '(15)' OR '&ENTRY' EQ '(R15)').STACK2
&RCPSTEP SETC  '&ENTRY'
         L     R15,&RCPSTEP            LOAD STACK ENTRY POINT ADDRESS
&R       SETC  '(15)'
         AGO   .STACK2
.TSTEP   AIF   ('&RCPSTEP' EQ '').STACK2
&R       SETC  '(15)'
         L     R15,&RCPSTEP            LOAD STACK ENTRY POINT ADDRESS
.STACK2  AIF   (&FLUSH).FLUSH
         STACK DELETE=&DELETE,STORAGE=&STORAGE,DATASET=&DATASET,       X
               MF=(E,&RCPPRE.IOPL),PARM=&RCPPRE.STPB,ENTRY=&R
         AGO   .STER
.FLUSH   STACK DELETE=ALL,                                             X
               MF=(E,&RCPPRE.IOPL),PARM=&RCPPRE.STPB,ENTRY=&R
.STER    AIF   ('&ERROR' EQ '').TSTER
         AIF   ('&ERROR' EQ 'NO').EXIT
&RCPSTER SETC  '&ERROR'
.STLTR   LTR   R15,R15                 TEST RETURN CODE
         BNZ   &RCPSTER                 AND BRANCH IF NON ZERO
         MEXIT ,
.TSTER   AIF   ('&RCPSTER' NE '').STLTR
         MEXIT ,
.GETLINE AIF   (&RCPGTPB(1)).GETL1
         MNOTE 4,'GETL Not Specified On BEGIN Macro, Errors May Occur'
.GETL1   AIF   ('&ENTRY' EQ '').TGTEP
         AIF   ('&ENTRY' EQ 'NO').GETL2
&R       SETC  '(15)'
         AIF   ('&ENTRY' EQ '(15)').GETL2
&RCPGTEP SETC  '&ENTRY'
         L     R15,&ENTRY              LOAD GETLINE ENTRY POINT
         AGO   .GETL2
.TGTEP   AIF   ('&RCPGTEP' EQ '').GETL2
&R       SETC  '(15)'
         L     R15,&RCPGTEP            LOAD GETLINE EP ADDRESS
.GETL2   MVI   &RCPPRE.GTPB+2,128     SET TGET FLAG
         GETLINE PARM=&RCPPRE.GTPB,MF=(E,&RCPPRE.IOPL),ENTRY=&R,       X
               INPUT=&INPUT,TERMGET=&TERMGET
         AIF   ('&ERROR' EQ '').TGTER
         AIF   ('&ERROR' EQ 'NO').GTLR
&RCPGTER SETC  '&ERROR'
.GTER    CL    R15,=F'4'               TEST RETURN CODE
         BH    &RCPGTER                 AND BRANCH IF ERROR
         AGO   .GTLR
.TGTER   AIF   ('&RCPGTER' NE '').GTER
.GTLR    ANOP  ,
&R       SETC  'R1'
         AIF   ('&Y' EQ '').GTLR2
&R       SETC  '&Y'
.GTLR2   L     &R,&RCPPRE.GTPB+GTPBIBUF-GTPB  LOAD INPUT LINE ADDRESS
         MEXIT ,
.PUTGET  AIF   (&RCPPGPB(1)).PGTL1
        MNOTE 4,'PUTGET Not Specified On BEGIN Macro, Errors May Occur'
.PGTL1   AIF   ('&ENTRY' EQ '').TPGEP
         AIF   ('&ENTRY' EQ 'NO').PGTL2
&R       SETC  '(15)'
         AIF   ('&ENTRY' EQ '(15)').PGTL2
         L     R15,&ENTRY              LOAD GETLINE ENTRY POINT
&RCPPGEP SETC '&ENTRY'
         AGO   .PGTL2
.TPGEP   AIF   ('&RCPPGEP' EQ '').PGTL2
&R       SETC  '(15)'
         L     R15,&RCPPGEP            LOAD PUTGET EP ADDRESS
.PGTL2   MVI   &RCPPRE.PGPB+10,128     SET TGET FLAG
         PUTGET PARM=&RCPPRE.PGPB,MF=(E,&RCPPRE.IOPL),ENTRY=&R,        X
               OUTPUT=&OUTPUT,TERMGET=&TERMGET,TERMPUT=&TERMPUT
         AIF   ('&ERROR' EQ '').TPGER
         AIF   ('&ERROR' EQ 'NO').PGLR
&RCPPGER SETC  '&ERROR'
.PGER    CL    R15,=F'4'               TEST RETURN CODE
         BH    &RCPPGER                 AND BRANCH IF ERROR
         AGO   .PGLR
.TPGER   AIF   ('&RCPPGER' NE '').PGER
.PGLR    ANOP  ,
&R       SETC  'R1'
         AIF   ('&Y' EQ '').PGLR2
&R       SETC  '&Y'
.PGLR2   L     &R,&RCPPRE.PGPB+PGPBIBUF-PGPB  LOAD INPUT LINE ADDRES
         MEND  ,
