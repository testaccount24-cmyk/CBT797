         MACRO ,
&NAME    #BLANK &F,&LEN=,&PAD=C' '
         LCLC  &L
         AIF   ('&F' EQ '').BLA0700   ERROR - MISSING FIELD
         AIF   ('&F'(1,1) EQ '(').BLA0100   FIELD IS A REGISTER
         AIF   ('&LEN' NE '').BLA0400   LENGTH SPECIFIED
&L       SETC  'L''&F'
&NAME    MVI   &F,&PAD
         MVC   &F+1(&L-1),&F
         MEXIT ,
.BLA0100 ANOP  ,
&NAME    LR    0,&F(1)                  R0 @ FIELD
         AIF   ('&LEN' NE '').BLA0200   LENGTH CODED
         LA    1,4095                   R1 HAS DEFAULT LENGTH
         AGO   .BLA0600                 CONTINUE
.BLA0200 AIF   ('&LEN'(1,1) EQ '(').BLA0300   LENGTH IS A REGISTER
         LA    1,&LEN                   R1 HAS LENGTH OF FIELD
         AGO   .BLA0600                 CONTINUE
.BLA0300 LR    1,&LEN(1)                R1 HAS LENGTH OF FIELD
         AGO   .BLA0600                 CONTINUE
.BLA0400 ANOP  ,
&NAME    LA    0,&F                     R0 @ FIELD
         AIF   ('&LEN'(1,1) EQ '(').BLA0500   LEN IS A REGISTER
         LA    1,&LEN                   R1 HAS LENGTH OF FIELD
         AGO   .BLA0600                 CONTINUE
.BLA0500 ANOP  ,
         LR    1,&LEN(1)                R1 HAS LENGTH OF FIELD
.BLA0600 ANOP  ,
         AIF   ('&PAD' EQ '0' OR '&PAD'  EQ 'X''00''').BLA0640
         LA    15,&PAD                  R15 HAS PAD CHARACTER
         SLL   15,24                    IN HIGH ORDER BYTE
         AGO   .BLA0660
.BLA0640 SR    15,15                    ZERO PAD AND LENGTH
.BLA0660 MVCL  0,14                     PROPOGATE PAD CHARACTER
         MEXIT ,
.BLA0700 MNOTE 8,'BLA001I - No Field Name Specified'
         MEND  .
