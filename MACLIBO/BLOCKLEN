         MACRO ,
&NAME    BLOCKLEN &DECB,&REG,&WKREG=R15,&ACSMETH=BSAM
         LCLC  &A,&B,&R,&O,&F
&O       SETC  '4'
&F       SETC  '128'
         AIF   ('&ACSMETH' EQ 'BSAM' OR '&ACSMETH' EQ 'BPAM').SETR
         AIF   ('&ACSMETH' NE 'BDAM').E2
&O       SETC  '5'
&F       SETC  '64'
.SETR    ANOP  ,
&R       SETC  '&REG'
         AIF   ('&REG' NE '').FDECB
&R       SETC  'R1'
.FDECB   AIF   ('&DECB' EQ '').E1
         AIF   ('&DECB'(1,1) EQ '(').RDECB
&A       SETC  '&DECB+'
&B       SETC  ''
         AGO   .START
.RDECB   ANOP  ,
&A       SETC  ''
&B       SETC  '(0,&DECB(1))'
.START   ANOP  ,
&NAME    TM    &A.&O&B,&F               WAS 'S' CODED ON READ?
         BO    *+12                      YES, GET BLOCKSIZE FROM DCB
         LH    &R,&A.6&B                 ELSE GET IT FROM DECB
         B     *+12                     SKIP DCB CODE
         L     &R,&A.8&B                GET DCB ADDRESS
         LH    &R,62(0,&R)              GET MAXIMUM BLOCKSIZE FROM DCB
         L     &WKREG,&A.16&B           LOAD IOB ADDRESS
         SH    &R,14(0,&WKREG)          SUBTRACT RESIDUAL COUNT
         MEXIT ,
.E1      MNOTE 8,'DECB ADDRESS OMITTED'
         MEXIT ,
.E2      MNOTE 4,'INVALID ACSMETH CODED, ''BSAM'' ASSUMED'
         AGO   .SETR
         MEND
