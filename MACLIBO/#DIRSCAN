         MACRO ,
&NAME    #DIRSCAN &DCB,&MEMPTR=,&NEXT=,&LAST=,&COUNTER=,&WKREG=
         LCLC  &L,&M,&N,&X
         AIF   ('&DCB' EQ '').DIR04     ERROR - NO DCB
         AIF   ('&MEMPTR' EQ '').DIR05  ERROR - NO MEMPTR
         AIF   ('&NEXT' EQ '').DIR06    ERROR - NO NEXT
         AIF   ('&LAST' EQ '').DIR07    ERROR - NO LAST
         AIF   ('&WKREG' EQ '').DIR08   ERROR - NO WORK REG
&L       SETC  '&WKREG'                 PICK UP WORK REGISTER
&M       SETC  '&MEMPTR'                MEMBER POINTER
&N       SETC  '&COUNTER'               COUNTER
&X       SETC  '&SYSNDX'                AND THE INDEX
&NAME    B     DIR&X.1                  GO READ FIRST DIRECTORY BLOCK
&NEXT    IC    R1,11(0,&M)              ISOLATE LENGTH
         SLL   R1,27                    OF NEXT ENTRY
         SRL   R1,26
         LA    &M,12(R1,&M)             POINT TO NEXT ENTRY
         CLR   &M,&L                    HAVE WE REACHED END OF BLOCK?
         BL    DIR&X.2                  NO, GO TEST FOR LAST ENTRY
DIR&X.1  GET   &DCB                     READ IN A DIRECTORY BLOCK
         AIF   ('&N' EQ '').DIR03       NO COUNTER, CONTINUE
         AIF   ('&N'(1,1) EQ '(').DIR02 COUNTER, IS A REG
         LA    R15,1                    INCREMENT COUNTER OF # BLOCKS
         A     R15,&N
         ST    R15,&N
         AGO   .DIR03
.DIR02   LA    &N(1),1(0,&N(1))         INCREMENT COUNTER OF # BLOCKS
.DIR03   LH    &L,0(0,R1)               GET LENGTH OF BLOCK
         LA    &L,0(R1,&L)              POINT TO LAST USED BYTE
         LA    &M,2(0,R1)               POINT TO FIRST ENTRY
DIR&X.2  CLI   0(&M),255                IS THIS END OF DIRECTORY ?
         BE    &LAST                    YES, TAKE EXIT
         MEXIT ,
.DIR04   IHBERMAC 1006,DCB
         MEXIT ,
.DIR05   IHBERMAC 1006,MEMPTR
         MEXIT ,
.DIR06   IHBERMAC 1006,NEXT
         MEXIT ,
.DIR07   IHBERMAC 1006,LAST
         MEXIT ,
.DIR08   IHBERMAC 1006,WKREG
         MEND  ,
