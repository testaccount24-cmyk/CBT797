         MACRO ,                   |
&LABEL   #TIME &TO,&FROM=,&FORMAT='HHMMSSTH',&CLOCK=24,&PAD='0',       *
               &LIST=YES
.*--------------------------------------------------------------------*
.*                                                                    *
.*       TO     (REQUIRED) - OUTPUT BUFFER (WORKTIME OR (R2-R15))     *
.*                                                                    *
.*       FROM=             - SUPPLIED TIME (FULLWORD OR (R0-R15))     *
.*                                                                    *
.*       FORMAT= HH        - HOUR (00-23)                             *
.*               MM        - MINUTES (00-60)                          *
.*               SS        - SECONDS (00-60)                          *
.*               TH        - TENTHS & HUNDREDTHS OF SECONDS           *
.*               AM|PM     - PART OF DAY (MORNING OR AFTERNOON)       *
.*                                                                    *
.*       CLOCK=  12|24     - STANDARD OR MILITARY CLOCK OPTION        *
.*                                                                    *
.*       PAD=    '0'       - 1 BYTE PAD CHARACTER FOR HH ENCLOSED     *
.*                           WITHIN QUOTES                            *
.*                                                                    *
.*       LIST=   YES|NO    - PRINT GENERATION OPTION                  *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLB  &STF(10)            |
         GBLC  &STPREF             |
         LCLA  &A,&B,&C,&D         |
         LCLB  &F,&G,&H,&I         |
         LCLC  &K,&P,&Q,&X,&REG    |
&STF(8)  SETB  1                   | INDICATE #STARTWA FIELDS WANTED
&X       SETC  'TM&SYSNDX'         | GENERATE LABEL PREFIX
&P       SETC  '&STPREF.DBL1'      | DOUBLE WORD WORKAREA #1
&Q       SETC  '&STPREF.DBL2'      | DOUBLE WORD WORKAREA #2
         AIF   ('&LIST' EQ 'YES').TIM0100
         AIF   ('&LIST' NE 'NO').TIMERR1
         PUSH  PRINT
         PRINT NOGEN
.TIM0100 ANOP  ,                   |
         AIF   ('&LABEL' EQ '').TIM0200
&LABEL   DS    0H                  #TIME MACRO - START
.TIM0200 ANOP  ,                   |
&A       SETA  K'&PAD              | LENGTH OF PAD FIELD
         AIF   (&A NE 3).TIMERR2   | IF NOT 3 BYTES, ERROR
*-------
*        #TIME INITIALIZATION
*-------
         AIF   ('&TO' EQ '').TIMERR3
         AIF   ('&CLOCK' EQ '12').TIM0300
         AIF   ('&CLOCK' NE '24').TIMERR4
.TIM0300 ANOP  ,                   |
         AIF   ('&FROM' EQ '').TIM0600
         AIF   ('&FROM'(1,1) EQ '(').TIM0400
         ICM   R0,15,&FROM         R0 HAS TIME - HHMMSSTH
         AGO   .TIM0500            |
.TIM0400 ANOP  ,                   |
&REG     SETC  '&FROM'(2,K'&FROM-2)
         AIF   ('&REG' EQ 'R0' OR '&REG' EQ '0').TIM0500
         LR    R0,&REG             R0 HAS TIME - HHMMSSTH
.TIM0500 ANOP  ,                   |
         C     R0,&X.D             CHECK FOR MAXIMUM TIME OF DAY
         BH    &X.A                B. IF TOO HIGH
         LTR   R0,R0               CHECK FOR MINIMUM TIME OF DAY
         BNZ   &X.B                B. IF NOT TOO LOW
&X.A     DS    0H
&F       SETB  1                   | INDICATE CONSTANTS REQUIRED
.TIM0600 ANOP  ,                   |
         TIME  DEC                 GET CURRENT TIME OF DAY
&X.B     DS    0H
         AIF   ('&TO'(1,1) EQ '(').TIM0700
         LA    R1,&TO              R1 HAS DESTINATION FIELD ADDRESS
         AGO   .TIM0800            |
.TIM0700 ANOP  ,                   |
&REG     SETC  '&TO'(2,K'&TO-2)
         LR    R1,&REG             R1 HAS DESTINATION FIELD ADDRESS
.TIM0800 ANOP  ,                   |
&A       SETA  K'&FORMAT-2         | LENGTH OF FORMAT FIELD
&B       SETA  0                   | INDEX INTO FORMAT
&K       SETC  '&FORMAT'(2,K'&FORMAT-2)
         MVC   0(&A,R1),&X.C
.TIM0900 ANOP  ,                   |
&C       SETA  &A-&B               | CALCULATE # OF REMAINING BYTES
         AIF   (&C LT 2).TIM2200   | IF 1 TO GO, EXIT
         AIF   ('&K'(&A-&C+1,2) EQ 'HH').TIM1000
         AIF   ('&K'(&A-&C+1,2) EQ 'MM').TIM1400
         AIF   ('&K'(&A-&C+1,2) EQ 'SS').TIM1600
         AIF   ('&K'(&A-&C+1,2) EQ 'TH').TIM1800
         AIF   ('&K'(&A-&C+1,2) EQ 'AM').TIM2000
         AIF   ('&K'(&A-&C+1,2) EQ 'PM').TIM2000
&B       SETA  &B+1                | POINT TO NEXT CHARACTER
&D       SETA  &D+1                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM1000 ANOP  ,                   |
*-------
*        CONVERT "HH" INTO HOURS
*-------
         XC    &P,&P               CLEAR WORK AREA #1
         XC    &Q,&Q               CLEAR WORK AREA #2
         AIF   (&D EQ 0).TIM1100   |
         LA    R1,&D.(,R1)         ADVANCE OUTPUT AREA POINTER
&D       SETA  0                   | RESET INDEX INTO AREA
.TIM1100 ANOP  ,                   |
         STCM  R0,8,&P+7           SAVE "HH"
         MVO   &Q+6(2),&P+7(1)
         OI    &Q+7,X'0F'          CONVERT TO "0HHF'
         AIF   ('&CLOCK' EQ '24').TIM1200
&G       SETB  1                   | INDICATE CONSTANTS REQUIRED
&I       SETB  1                   | INDICATE CONSTANTS REQUIRED
         CL    R0,&X.E             CHECK IF CURRENT TIME IS PAST 12:59
         BL    *+14                B. IF NOT
         SP    &Q+6(2),&X.G
         OI    &Q+7,X'0F'          CONVERT TO "0HHF'
.TIM1200 ANOP  ,                   |
         UNPK  0(2,R1),&Q+6(2)
&REG     SETC  '&PAD'(2,K'&PAD-2)  |
         AIF   ('&REG' EQ '0').TIM1300
         CLI   0(R1),C'0'          CHECK IF 1ST BYTE IS ZERO
         BNE   *+8                 B. IF NOT
         MVI   0(R1),C&PAD         INSERT PAD CHARACTER
.TIM1300 ANOP  ,                   |
&B       SETA  &B+2                | POINT TO NEXT CHARACTER
&D       SETA  &D+2                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM1400 ANOP  ,                   |
*-------
*        CONVERT "MM" INTO MINUTES
*-------
         XC    &P,&P               CLEAR WORK AREA #1
         XC    &Q,&Q               CLEAR WORK AREA #2
         AIF   (&D EQ 0).TIM1500   |
         LA    R1,&D.(,R1)         ADVANCE OUTPUT AREA POINTER
&D       SETA  0                   | RESET INDEX INTO AREA
.TIM1500 ANOP  ,                   |
         STCM  R0,4,&P+7           SAVE "MM"
         MVO   &Q+6(2),&P+7(1)
         OI    &Q+7,X'0F'          CONVERT TO "0MMF'
         UNPK  0(2,R1),&Q+6(2)
&B       SETA  &B+2                | POINT TO NEXT CHARACTER
&D       SETA  &D+2                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM1600 ANOP  ,                   |
*-------
*        CONVERT "SS" INTO SECONDS
*-------
         XC    &P,&P               CLEAR WORK AREA #1
         XC    &Q,&Q               CLEAR WORK AREA #2
         AIF   (&D EQ 0).TIM1700   |
         LA    R1,&D.(,R1)         ADVANCE OUTPUT AREA POINTER
&D       SETA  0                   | RESET INDEX INTO AREA
.TIM1700 ANOP  ,                   |
         STCM  R0,2,&P+7           SAVE "SS"
         MVO   &Q+6(2),&P+7(1)
         OI    &Q+7,X'0F'          CONVERT TO "0SSF'
         UNPK  0(2,R1),&Q+6(2)
&B       SETA  &B+2                | POINT TO NEXT CHARACTER
&D       SETA  &D+2                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM1800 ANOP  ,                   |
*-------
*        CONVERT "TH" INTO TENTHS & HUNDREDTHS OF SECONDS
*-------
         XC    &P,&P               CLEAR WORK AREA #1
         XC    &Q,&Q               CLEAR WORK AREA #2
         AIF   (&D EQ 0).TIM1900   |
         LA    R1,&D.(,R1)         ADVANCE OUTPUT AREA POINTER
&D       SETA  0                   | RESET INDEX INTO AREA
.TIM1900 ANOP  ,                   |
         STCM  R0,1,&P+7           SAVE "TH"
         MVO   &Q+6(2),&P+7(1)
         OI    &Q+7,X'0F'          CONVERT TO "0THF'
         UNPK  0(2,R1),&Q+6(2)
&B       SETA  &B+2                | POINT TO NEXT CHARACTER
&D       SETA  &D+2                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM2000 ANOP  ,                   |
*-------
*        CONVERT "AM" OR "PM" INTO THE CURRENT PART OF THE DAY
*-------
         AIF   (&D EQ 0).TIM2100   |
         LA    R1,&D.(,R1)         ADVANCE OUTPUT AREA POINTER
&D       SETA  0                   | RESET INDEX INTO AREA
.TIM2100 ANOP  ,                   |
&H       SETB  1                   | INDICATE CONSTANTS REQUIRED
         MVI   0(R1),C'A'          SET TO "AM"
         CL    R0,&X.F             CHECK IF CURRENT TIME IS PAST NOON
         BL    *+8                 B. IF NOT
         MVI   0(R1),C'P'          CHANGE TO "PM"
&B       SETA  &B+2                | POINT TO NEXT CHARACTER
&D       SETA  &D+2                | POINT TO NEXT CHARACTER
         AGO   .TIM0900            |
.*                                 |
.TIM2200 ANOP  ,                   |
         B     &X.H                BRANCH AROUND CONSTANTS
*-------
*        #TIME CONSTANTS
*-------
&X.C     DC    C&FORMAT            FORMAT FIELD OVERLAY
         AIF   (NOT &F).TIM2300    | BYPASS UNNECESSARY CONSTANTS
&X.D     DC    A(X'23595999')      MAXIMUM TIME VALUE PER DAY
.TIM2300 ANOP  ,                   |
         AIF   (NOT &G).TIM2400    | BYPASS UNNECESSARY CONSTANTS
&X.E     DC    A(X'12595999')      MAXIMUM TIME TILL 1PM
.TIM2400 ANOP  ,                   |
         AIF   (NOT &H).TIM2500    | BYPASS UNNECESSARY CONSTANTS
&X.F     DC    A(X'12000000')      MAXIMUM TIME TILL PM
.TIM2500 ANOP  ,                   |
         AIF   (NOT &I).TIM2600    | BYPASS UNNECESSARY CONSTANTS
&X.G     DC    PL2'+12'            VALUE TO SUBTRACT IF TIME PAST 12:59
.TIM2600 ANOP  ,                   |
&X.H     DS    0H                  #TIME MACRO - END
         AGO   .MEND
.TIMERR1 MNOTE 8,'#TIM01A - "LIST=" Parameter Is Invalid (YES Or NO)'
         AGO   .MEND               |
.TIMERR2 MNOTE 8,'#TIM02A - "PAD=" Parameter Is Invalid'
         AGO   .MEND               |
.TIMERR3 MNOTE 8,'#TIM03A - "TO=" Parameter Is Missing'
         AGO   .MEND               |
.TIMERR4 MNOTE 8,'#TIM04A - "CLOCK=" Parameter Is Invalid (12 OR 24)'
         AGO   .MEND               |
.MEND    ANOP  ,                   |
         AIF   ('&LIST' EQ 'YES').MEND2
         POP   PRINT
.MEND2   ANOP  ,                   |
         MEND  ,                   |
