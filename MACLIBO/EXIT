         MACRO ,
&NAME    EXIT  &RC=,&IKJRLSA=YES,&R14=
         GBLA  &RCPSWS(10)              ENVIRONMENT SWITCHES
         GBLB  &RCPPPL(2)
         GBLC  &RCPPRE,&RCPWKCS,&RCPWKDS,&RCPSPN
         GBLC  &RCPCLNP
         LCLC  &P
&P       SETC  '&RCPPRE'
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  AIF   ('&P' EQ '').TRLSA
&P.EXIT  DS    0H
.TRLSA   AIF   (NOT &RCPPPL(1) OR '&IKJRLSA' EQ 'NO').NRLSA
***********************************************************************
**       FREE THE PARSE PDL WORKAREA                                 **
***********************************************************************
         IKJRLSA &P.PDLP
.NRLSA   AIF   ('&RCPWKCS&RCPWKDS' NE '').WAOPT  TEST FOR WORKAREA OPT
         AIF   ('&RC' EQ '(R15)' OR '&RC' EQ '(15)').RET1
         AIF   ('&RC' EQ '0' OR '&RC' EQ '').ZEROREG
         AIF   ('&RC'(1,1) EQ '(').REGRC
         LA    R15,&RC                  LOAD RETURN CODE
         AGO   .RET1
.REGRC   LR    R15,&RC(1)               LOAD RETURN CODE
         AGO   .RET1
.ZEROREG SLR   R15,R15                  RETURN CODE = 0
.RET1    AIF   (&RCPSWS(1) GT 0).RET2 NOSAVE/NOSAVEAREA OPT
         L     R13,4(0,R13)             RESTORE CALLERS SAVE AREA
.RET2    AIF   (&RCPSWS(1) EQ 2).RET3  NOSAVE OPTION
         AIF   ('&R14' NE '').LMR012
         L     R14,12(0,R13)            RESTORE REGISTER  14
.LMR012  LM    R0,R12,20(R13)           RESTORE REGISTERS 0-12
.RET3    AIF   ('&R14' EQ '').RET4
         AIF   ('&R14'(1,1) EQ '(').R14R
         L     R14,&R14                 RESTORE REGISTER  14
         AGO   .RET4
.R14R    LR    R14,&R14(1)              RESTORE REGISTER  14
.RET4    BR    R14                      RETURN TO CALLER
         MEXIT ,
.WAOPT   AIF   ('&RC' EQ '').RCOK
         MNOTE 4,'RC= Option Invalid With Current BEGIN Options,'
         MNOTE *,'    Use SETRC Macro To Set The Return Code'
.RCOK    AIF   ('&RCPWKCS' NE '').CSOPT WORK CSECT OPTION
         AIF   (&RCPSWS(2) EQ 1 AND &RCPSWS(3) EQ 0).CSOPT
         L     R15,&P.RCODE             LOAD RETURN CODE
         AIF   (&RCPSWS(2) EQ 2).PROCM   PROC(MAIN) OPT
         LR    R1,R13                   SAVE WORK AREA ADDRESS
         L     R13,4(0,R13)             RESTORE CALLER'S SAVE AREA
         ST    R15,16(0,R13)            STORE RETURN CODE
         AIF   (&RCPSWS(3) EQ 1).LIFOF      LIFO OPTION
         AGO   .FREE                    WORKAREA TO BE FREED
.CSOPT   L     R15,&P.RCODE             LOAD RETURN CODE
         L     R13,4(0,R13)             RESTORE CALLER'S SAVE AREA
         L     R14,12(0,R13)            RESTORE REGISTER  14
         LM    R0,R12,20(R13)           RESTORE REGISTERS 0-12
         BR    R14                      RETURN TO CALLER
         MEXIT ,
.FREE    FREEMAIN RU,LV=&P.WKLEN,A=(1),SP=&RCPSPN
         AGO   .LM
.LIFOF   ANOP  ,
*        #FREE A=(R1)                  RETURN WORKAREA TO STACK
         #FREE A=(R1)
         AGO   .LM
.PROCM   L     R1,0(0,R13)              GET MCA ADDRESS
         L     R1,&P.XDS-&P.MCA(,R1)    LOAD DUMMY SECTION ADDR
         L     R13,4(0,R13)             RESTORE CALLER'S SAVE AREA
         ST    R15,16(0,R13)            STORE RETURN CODE
         L     R0,&P.CXD                LOAD WORK AREA LENGTH
         FREEMAIN RU,LV=(0),A=(1),SP=&RCPSPN
.LM      LM    R14,R12,12(R13)          RESTORE THE REGISTERS
         BR    R14                      RETURN TO CALLER
         MEND  ,
