         MACRO
&NAME    #MVCL &TO,&FROM,&LEN=
.*
.*       MVCL USING REGISTERS - 0,1 & 14,15
.*
         LCLC  &L,&REG
         AIF   ('&FROM' EQ '').MVC0900  ERROR - MISSING FIELD
         AIF   ('&TO' EQ '').MVC1000    ERROR - MISSING FIELD
         AIF   ('&NAME' EQ '').MVC0100
&NAME    DS    0H
.*
.MVC0100 ANOP
         AIF   ('&TO'(1,1) EQ '(').MVC0200 FIELD IS A REGISTER
         LA    0,&TO               >>---> OUTPUT FIELD
         AGO   .MVC0300
.MVC0200 ANOP
&REG     SETC  '&TO'(2,K'&TO-2)
         AIF   ('&REG' EQ '0' OR '&REG' EQ 'R0').MVC0300
         AIF   ('&REG' EQ 'r0').MVC0300
         LR    0,&REG              >>---> OUTPUT FIELD
.MVC0300 ANOP
         AIF   ('&LEN' NE '').MVC0400
         AIF   ('&TO'(1,1) EQ '(').MVC1100
         LA    1,L'&TO             LOAD OUTPUT LENGTH
         AGO   .MVC0600             CONTINUE
.MVC0400 ANOP
         AIF   ('&LEN'(1,1) EQ '(').MVC0500   LENGTH IS A REGISTER
         LA    1,&LEN              LOAD OUTPUT LENGTH
         AGO   .MVC0600             CONTINUE
.MVC0500 ANOP
&REG     SETC  '&LEN'(2,K'&LEN-2)
         AIF   ('&REG' EQ '1' OR '&REG' EQ 'R1').MVC0600
         AIF   ('&REG' EQ 'r1').MVC0600
         LR    1,&REG              LOAD OUTPUT LENGTH
.MVC0600 ANOP  ,
         AIF   ('&FROM'(1,1) EQ '(').MVC0700 FIELD IS A REGISTER
         LA    14,&FROM            >>---> INPUT FIELD
         AGO   .MVC0800
.MVC0700 ANOP
&REG     SETC  '&FROM'(2,K'&FROM-2)
         AIF   ('&REG' EQ '14' OR '&REG' EQ 'R14').MVC0800
         AIF   ('&REG' EQ 'r14').MVC0800
         LR    14,&REG             >>---> INPUT FIELD
.MVC0800 ANOP  ,
         LR    15,1                COPY INPUT LENGTH TO OUTPUT LENGTH
         MVCL  0,14                MOVE CHARACTER LONG
         MEXIT
.MVC0900 MNOTE 8,'MVC001A - "TO" FIELD NOT SPECIFIED'
         MEXIT
.MVC1000 MNOTE 8,'MVC002A - "FROM" FIELD NOT SPECIFIED'
         MEXIT
.MVC1100 MNOTE 8,'MVC002A - "LENGTH" NOT SPECIFIED'
         MEND
