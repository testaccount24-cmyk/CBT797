         MACRO
&NAME    #TSOTERM &OBTAIN,&IF3270=,&IFTWX=,&IF2741=
         AIF  ('&OBTAIN' EQ '' OR '&OBTAIN' EQ 'UCB' OR                X
               '&OBTAIN' EQ 'TYPE').OOKAY
         IHBERMAC 1001,OBTAIN,&OBTAIN
         MEXIT ,
.OOKAY   ANOP  ,
&NAME    L     0,=X'FF000000'           TERMTYPE ENTRY CODE
         SVC   94                       TERMINAL CONTROL SVC
         AIF   ('&IF3270&IFTWX&IF2741' NE '').TYPE
         AIF   ('&OBTAIN' EQ 'TYPE').TYPE
         AIF   ('&OBTAIN' NE 'UCB').MEND
         N     15,=X'FFFF0000'          PICK UP UCB
         MEXIT ,
.TYPE    N     15,=X'000000FF'          PICK UP TERMINAL TYPE
         AIF   ('&IF2741' EQ '').T3270
         CLM   15,1,=AL1(12)            IS THIS A 2741 ?
         BE    &IF2741                  BRANCH IF YES
.T3270   AIF   ('&IF3270' EQ '').TTWX
         CLM   15,1,=AL1(4)             IS THIS A 3270 ?
         BE    &IF3270                  BRANCH IF YES
.TTWX    AIF   ('&IFTWX'  EQ '').MEND
         CLM   15,1,=AL1(16)            IS THIS A TWX ?
         BE    &IFTWX                   BRANCH IF YES
.MEND    MEND  ,
